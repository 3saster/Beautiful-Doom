Class BD_Revenant : BDoomMonster replaces Revenant
{
Default {
		Health 300;
		Radius 20;
		Height 56;
		Mass 500;
		Speed 10;
		PainChance 100;
		Monster;
		MeleeThreshold 196;
		+MISSILEMORE 
		+FLOORCLIP
		SeeSound "skeleton/sight";
		PainSound "skeleton/pain";
		DeathSound "skeleton/death";
		ActiveSound "skeleton/active";
		MeleeSound "skeleton/melee";
		HitObituary "$OB_UNDEADHIT";
		Obituary "$OB_UNDEAD";
		Tag "$FN_REVEN";
	gibhealth 60;
	BDoomMonster.allowfiredeath false;
	}
states
	{
	Spawn:
		REVI AB 10 A_Look();
		loop;
	See:
		RSKE abcddefghh 2 A_Chase();
		loop;
	Melee:
		TNT1 A 0 A_FaceTarget();
		SSKE A 3 A_SkelWhoosh();
		SSKE B 3;
		SSKE C 3 A_FaceTarget();
		SSKE D 3;
		SSKE E 3 A_SkelFist();
		SSKE D 3;
		goto See;
	Missile:
		TNT1 A 0 A_FaceTarget();
		RMIS a 4 a_facetarget();
		
		TNT1 A 0 A_Jump(128,"MissileLeft");
		RMIR b 3;
		RMIR c 3;
		RMIR d 3 a_skelmissile();
		RMIR e 3;
		RMIR F 2;
		goto MissileEnd;
	MissileLeft:	
		RMIL b 3;
		RMIL c 3;
		RMIL d 3 a_skelmissile();
		RMIL e 3;
		RMIL F 2;
		goto MissileEnd;
	MissileEnd:	
		RMIS G 2;
		RMIS H 10 A_facetarget();
		goto See;
	Crush:
		TNT1 A 0 { scale.x *= randompick(-1,1); }
		CRS2 A -1;
		stop;
	Pain:
		REVP a 5;
	    REVP b 5 A_Pain();
		goto See;
	Death:
		TNT1 A 0 { 
			scale.x *= randompick(-1,1);
			if (random(1,2) == 2)
				return ResolveState("Death1");
			return ResolveState(null);
			}
		REVM abc 3;
		REVM d 3 a_scream();
		REVM e 2 a_noblocking();
		REVM e 1;
		REVM fghi 3;
		REVM i -1;
		stop;
	Death1:
		REVN abc 3;
		REVN d 4 a_scream();
		REVN e 4 a_noblocking();
		REVN fg 4;
		REVN H -1;
		stop;	
	XDeath:
		TNT1 A 0 { 
			if (random(1,2) == 2)
				return ResolveState("Death");
			scale.x *= randompick(-1,1);
			return ResolveState(null);
			}
		REVX a 3;
		REVX b 3;
		REVX c 2 a_scream();
		REVX d 2 a_noblocking();
		REVX efghij 3;
		REVX k -1;
		}
}


Class RevenantTracerSmokeTrail : Rocket1Trail replaces RevenantTracerSmoke //Bulletpuffs are still spawned behind the rocket because it's hardcoded in A_Tracer
{
Default {
	alpha 0.45;
	scale 0.12;
	}
}


Class BD_RevenantTracer : RevenantTracer replaces RevenantTracer
{
states
	{
	Spawn:
		TNT1 A 0 NoDelay {
			A_PlaySound("revenant/tracerfly",4,true,3);
			if (bdoom_debris)
				A_spawnItemEx("BD_RevenantTracerFlare",flags:SXF_SETMASTER|SXF_ORIGINATOR);
			}
		SKEB AB 2  A_Tracer();
		goto spawn+1;
	Death:
		TNT1 A 0 {
			A_StopSound(4);
			bFORCEXYBILLBOARD = true;
			alpha = 0.8;
			}
		FBXP ABC 3  A_FadeOut(0.1);
		FBXP DEF 4  A_FadeOut(0.1);
		stop;
	}
}

Class BD_RevenantTracerFlare : Flare_General
{
Default {
	renderstyle "Add";
	alpha 0.8;
	scale 0.11;
	}
override void Tick() {
	super.Tick();
	if (bdoom_debris && master)
		SetOrigin(master.pos,1);
	else
		self.Destroy();
	}
states
	{
	Spawn:
		LENY A 1 bright;
		wait;
	}
}
