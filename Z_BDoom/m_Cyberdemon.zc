Class BD_CyberDemon : BDoomMonster //replaces CyberDemon
{
Default
	{
	Health 4000;
	Radius 40;
	Height 110;
	Mass 1000;
	Speed 16;
	PainChance 20;
	Monster;
	MinMissileChance 160;
	+BOSS 
	+MISSILEMORE
	+FLOORCLIP
	+NORADIUSDMG
	+DONTMORPH
	+BOSSDEATH
	SeeSound "cyber/sight";
	PainSound "cyber/pain";
	DeathSound "cyber/death";
	ActiveSound "cyber/active";
	Obituary "$OB_CYBORG";
	Tag "$FN_CYBER";
	}
states
	{
	Raise:
		stop;
	Death.Fire:
		stop;
	Death.Greenfire:
		stop;
	Death.PlasmaDamage:
		stop;
	Death.BFGSplash:
		stop;
	Spawn:
		CBDM A 10 A_Look;
		loop;
	see:
		CBDM a 3 a_hoof;
		CBDM bcdef 3 a_chase;
		CBDM g 3 a_metal;
		CBDM h 3 a_chase;
		loop;
		
	Missile:
		CBDS abc 2 a_facetarget;
		CBDS d 3 {
			if (bdoom_weapons > 0)
				A_SpawnProjectile("Rocket",56,-24);
			else
				A_SpawnProjectile("Rocket",56);
			}
		CBDS e 3;
		CBDS c 6;
		CBDS abc 4 a_facetarget;
		CBDS d 3 {
			if (bdoom_weapons > 0)
				A_SpawnProjectile("Rocket",56,-24);
			else
				A_SpawnProjectile("Rocket",56);
			}
		CBDS e 3;
		CBDS c 6;
		CBDS abc 4 a_facetarget;
		CBDS d 3 {
			if (bdoom_weapons > 0)
				A_SpawnProjectile("Rocket",56,-24);
			else
				A_SpawnProjectile("Rocket",56);
			}
		CBDS e 3;
		CBDS c 6;
		goto see;
	pain:
		CBDI a 4 a_pain;
		CBDI bc 3;
		goto see;
	
	Death:
		TNT1 A 0 {
			if (bdoom_gibs && !bdoom_nobossgibs)
				return ResolveState("Death.Particles");
			return ResolveState(null);
			}
		CBDD a 13;
		CBDD b 5 a_scream;
		CBDD cdefghij 4;
		CBDD k 4 a_noblocking ;
		CBDD l 30;
		CBDD l -1 a_bossdeath;
		stop;
	Death.Particles:
		TNT1 A 0 A_PlaySound("cyberdemon/death",6);
		CBDI AABCAABCAA 5 A_SpawnItemEx("CyberExplosion",frandom(-24,24),frandom(-24,24),frandom(32,104),flags:SXF_SETTARGET,failchance:48);
		TNT1 A 0 A_NoBlocking;
		CBDI BCAABC 5 A_SpawnItemEx("CyberExplosion",frandom(-24,24),frandom(-24,24),frandom(32,104),flags:SXF_SETTARGET,failchance:36);
		TNT1 A 0 {
			if (bdoom_shakes)
				Radius_Quake(5,20,0,13,0);
			A_PlaySound("cyberdemon/explosion",5,attenuation:ATTN_NONE);
			}
		CBDD AAABBCBCCDDEEFFGG 1 BD_Bleed();
		TNT1 A 0 {
			A_FaceTarget();
			A_PlaySound("gibs/gibbed");
			A_SpawnItemEx("CyberHead",0,-8,80, frandom(-2,2),frandom(-2,2),frandom(5,9),flags:SXF_SETTARGET);
			A_SpawnItemEx("CyberHandLeft",0,-36,64, frandom(-1.5,1.5),frandom(-3.5,-5.5),frandom(4,6),flags:SXF_SETTARGET);
			A_SpawnItemEx("CyberHandRight",0,36,64, frandom(-1.5,1.5),frandom(3.5,5.5),frandom(4,6),flags:SXF_SETTARGET);
			A_SpawnItemEx("CyberLegLeft",0,-22,14, frandom(-1.5,1.5),frandom(-3.5,-5.5),frandom(2.5,4),flags:SXF_SETTARGET);
			A_SpawnItemEx("CyberLegRight",0,24,14, frandom(-1.5,1.5),frandom(3.5,5.5),frandom(2.5,4),flags:SXF_SETTARGET);
			
			A_SpawnItemEx("CyberTorso1",0,36,64, frandom(-3,3),frandom(2,4),frandom(5,7),flags:SXF_SETTARGET);
			A_SpawnItemEx("CyberTorso2",0,-36,64, frandom(-3,3),frandom(-2,-4),frandom(5,7),flags:SXF_SETTARGET);
			A_SpawnItemEx("CyberTorso3",0,0,26, frandom(-4,4),frandom(-4,4),frandom(3,6),flags:SXF_SETTARGET);
					
			for (int i = 10; i > 0; i--)
				A_SpawnItemEx("BFountainSpray",frandom(-5.0,5.0),frandom(-5.0,5.0),random(24,72),frandom(-1.5,1.5),frandom(-1.5,1.5),frandom(4.5,6.5),0,SXF_SETTARGET,36);
			for (int i = 6; i > 0; i--)
				A_SpawnItemEx("BasicMeatPiece_Heavy",random(-4,4),random(-4,4),random(24,72),	frandom(-5,5),frandom(-5,5),random(5,8),	0,	SXF_SETTARGET, 64);
			for (int i = 6; i > 0; i--)
				A_SpawnItemEx("BasicMetalPiece",random(-4,4),random(-4,4),random(24,72),		frandom(-10,10),frandom(-10,10),random(5,10),	0,SXF_SETTARGET, 64);
			}
		CBDO HHHIIIJJJ 1 {
			if (bdoom_gibs) {
				for (int i = 4; i > 0; i--)
					A_SpawnItemEx("BFountainSpray",frandom(-5.0,5.0),frandom(-5.0,5.0),random(24,72),frandom(-1.5,1.5),frandom(-1.5,1.5),frandom(4.5,6.5),0,SXF_SETTARGET,36);
				for (int i = 2; i > 0; i--)
					A_SpawnItemEx("BasicMeatPiece_Heavy",random(-4,4),random(-4,4),random(24,72),	frandom(-5,5),frandom(-5,5),random(5,8),	0,	SXF_SETTARGET, 64);
				for (int i = 2; i > 0; i--)
					A_SpawnItemEx("BasicMetalPiece",random(-4,4),random(-4,4),random(24,72),		frandom(-10,10),frandom(-10,10),random(5,10),	0,SXF_SETTARGET, 64);
				}
			}
		CBDO K 3;
		CBDO L 150;
		CBDO L -1 A_BossDeath;
		stop;
		}
}

Class CyberExplosion : Flare_General
{
Default {
	+ROLLSPRITE
	renderstyle "Add";
	alpha 0.85;
	}
override void PostBeginPlay() {
	super.PostBeginPlay();
	A_SetScale(frandom(0.65,1.0));
	roll = random(0,359);
	}
states
	{
	Spawn:
		TNT1 A 0 NoDelay{
			A_PlaySound("weapons/rlan/explosion",1,1.0);
			if (bdoom_shakes)
				Radius_Quake(2,8,0,8,0);
			if(bdoom_blood) {
				for (int i = 6; i > 0; i--)
					A_SpawnItemEx("BD_Blood",0,0,0,	frandom(-3,3),frandom(-3,3),frandom(3,6),flags:SXF_SETTARGET,failchance:92);
				}
			if(bdoom_debris && target)
				A_SpawnItemEx("BasicMetalPiece",random(-4,4),random(-4,4),random(10,48),		frandom(5,8),frandom(-2,2),random(4,7),	AngleTo(target)*random(170,190),SXF_SETTARGET, 144);	
			}
		BOM3 ABCDEFGHIJKLMNOPRTU 1 bright A_FadeOut(0.01);
		stop;
		}
}
