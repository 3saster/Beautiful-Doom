Class BD_Cacodemon : BDoomMonster  replaces Cacodemon
{
Default
	{
	Health 400;
	Radius 31;
	Height 56;
	Mass 400;
	Speed 8;
	PainChance 128;
	Monster;
	+FLOAT +NOGRAVITY
	SeeSound "caco/sight";
	PainSound "caco/pain";
	DeathSound "caco/death";
	ActiveSound "caco/active";
	Obituary "$OB_CACO";
	HitObituary "$OB_CACOHIT";
	Tag "$FN_CACO";
	+FORCEXYBILLBOARD
	bloodcolor "00 00 CC";
	gibhealth 50;
	+FLOATBOB
	FloatBobStrength 0.35;
	}
override void Tick() {
	super.Tick();
	if (target)
		bFLOATBOB = false;
	}
states
	{
	Spawn:
		HEA1 AAAAA 10 A_Look();
		HEA1 B 5 A_Look();
		HEA1 C 5;
		HEA1 B 5 A_Look();
		HEA1 A 5;
		loop;
	See:
		HEA1 AAAAAAAAAA 3 A_Chase();
		HEA1 B 3 A_Chase();
		HEA1 C 3 A_Chase();
		HEA1 B 3 A_Chase();
		HEA1 A 3 A_Chase();
		loop;
	Missile:
		HEAA ABCCDD 1 A_FaceTarget();
		HEAA E 4  A_HeadAttack();
		HEAA CCA 1;
		Goto See;
	Pain:
		HEAA F 3;
		HEAA G 3 A_Pain();
		HEAA HGF 2;
		Goto See;
	Crush:
		CRS2 B -1;
		stop;
	Death:
		TNT1 A 0 {
			scale.x*=randompick(-1,1);
			bFORCEXYBILLBOARD = false;
			A_ChangeFlag("FLOATBOB",0);
			if (burning)
				return ResolveState("Death.Vanilla");
			return A_jump(212,"Death.Vanilla","Death1");
			if (bdoom_gibs)
				return A_Jump(128,"Death2");
			return ResolveState(null);	
			}
	Death.Vanilla:	
		HEAM A 3 BD_Bleed();
		HEAM B 3 A_Scream();
		HEAM CD 3 BD_Bleed();
		HEAM E 3 A_NoBlocking();
		HEAM F 3 A_SetFloorClip();
		HEAM GHI 3;
		goto deathbleed;
	Death1: //no eye, intestines fall down
		HEAN AB 3;
		TNT1 A 0 A_Scream();
		HEAN C 3  A_SpawnItemEx("CacoEye",frandom(-2,2),frandom(-2,2),frandom(28,32),frandom(3,7),0,frandom(0,5),frandom(-10,10),SXF_SETTARGET);
		 TNT1 A 0 BD_Bleed();
		HEAN DE 3;
		HEAN F 3 A_NoBlocking();
		 TNT1 A 0 BD_Bleed();
		HEAN GHIJKL 3;
		TNT1 A 0 A_SetFloorClip();
		HEAN MNO 5;
		goto deathbleed;
	Death2: //puke gibs, uncommon death
		TNT1 A 0 { bNOGRAVITY = true; }
		HEAP ABC 4;
		TNT1 A 0 A_SpawnItemEx("CacoEye",frandom(-2,2),frandom(-2,2),frandom(28,32),frandom(3,7),0,frandom(0,5),frandom(-10,10),SXF_SETTARGET);
		HEAP DDEEFFGGHH 2 A_SpawnItemEx("CacodemonMeatPiece",0,frandom(-6,6),frandom(8,20),	frandom(3,5),0,frandom(2,3),	random(-15,15),	SXF_SETTARGET,48);
		TNT1 A 0 {
			A_NoBlocking();
			bNOGRAVITY = false;
			}
		HEAP I 4 A_SetFloorClip();
		HEAP JKL 4;
		goto deathbleed;
	Death.Saw: //sliced diagonally
		TNT1 A 0 A_Jump(128,"Death");
		HEAM A 3 BD_Bleed();
		HEAM B 3 A_Scream();
		TNT1 A 0 A_FaceTarget();
		HEAO AABBCCDD 2 A_SpawnItemEx("BloodSpurt",	1,0,frandom(30,38),0,0,0,random(-40,40),SXF_SETTARGET,96);
		TNT1 A 0 {
			A_NoBlocking();
			A_SetFloorClip();
			}
		HEAO EFG 4;
		goto deathbleed;
	XDeath: //sprite gibbing from Smooth Doom
		TNT1 A 0 A_Jump(128,"Death");
		HEAX ABC 3 {
			if (bdoom_gibs)
				A_SpawnItemEx("CacodemonMeatPiece",frandom(-6,6),frandom(-6,6),frandom(8,20),	frandom(-6,6),frandom(-6,6),frandom(4,6), 0,	SXF_SETTARGET,48);
			}
		TNT1 A 0 A_XScream();
		HEAX DEF 3 {
			if (bdoom_gibs)
				A_SpawnItemEx("CacodemonMeatPiece",frandom(-6,6),frandom(-6,6),frandom(8,20),	frandom(-6,6),frandom(-6,6),frandom(4,6), 0,	SXF_SETTARGET,48);
			}
		TNT1 A 0 {
			A_NoBlocking();
			A_SetFloorClip();
			}
		HEAX GH 2 {
			if (bdoom_gibs)
				A_SpawnItemEx("CacodemonMeatPiece",frandom(-6,6),frandom(-6,6),frandom(8,20),	frandom(-6,6),frandom(-6,6),frandom(4,6), 0,	SXF_SETTARGET,48);
			}
		HEAX IJKLMNOPQRS 2;
		goto deathbleed;
	}
}

Class BD_CacodemonBall : CacodemonBall replaces CacodemonBall
{
Default {
	+FORCEXYBILLBOARD
	+DONTBLAST
	decal "Scorch";
	renderstyle "Add";
	alpha 0.9;
	scale 0.5;
	}
States
	{
	Spawn:
		CABA ABCD 2 bright NoDelay {
			if (bdoom_debris) {
				A_SpawnItemEx("FirePiece_Cacodemon",frandom(8,12),frandom(-5,5),frandom(-5,5),random(1,0),0,0,180,0);
				A_SpawnItemEx("FirePiece_Cacodemon",frandom(8,12),frandom(-5,5),frandom(-5,5),random(1,0),0,0,180,0);
				}
			}
		loop;
	Death:
		TNT1 a 0 A_SetScale(1.0,1.0);
		CABX ABCDEF 3  A_FadeOut(0.1);
		stop;
	}
}

Class FirePiece_Cacodemon : Flare_General
{
Default {
	alpha 0.5;
	scale 0.04;
	}
states
	{
	Spawn:
		SPRK C 1 bright Nodelay {
			A_FadeOut(0.01);
			scale*=0.95;
			}
		wait;
	}
}

/*
Actor FirePiece_Cacodemon : FirePiece
{
translation "169:191=192:207", "208:223=169:191"
states
	{
	Spawn:
		FIRP ABCDEFGHIJ 2  A_FadeOut(0.1)
		stop
	}
}