Class BD_Cacodemon : BDoomMonster  //replaces Cacodemon
{
Default
	{
	species "Cacodemon";
	Health 400;
	Radius 31;
	Height 56;
	Mass 400;
	Speed 8;
	PainChance 128;
	Monster;
	+FLOAT +NOGRAVITY
	SeeSound "caco/sight";
	PainSound "caco/pain";
	DeathSound "caco/death";
	ActiveSound "caco/active";
	Obituary "$OB_CACO";
	HitObituary "$OB_CACOHIT";
	Tag "$FN_CACO";
	+FORCEXYBILLBOARD
	bloodcolor "00 00 CC";
	gibhealth 50;
	+FLOATBOB
	FloatBobStrength 0.35;
	BDoomMonster.smokeheight 30;
	}
override void Tick() {
	super.Tick();
	if (!InStateSequence(curstate,FindState("Spawn")))
		bFLOATBOB = false;
	}
states
	{
	Spawn:
		HEA1 AAAAA 10 A_Look();
		HEA1 B 5 A_Look();
		HEA1 C 5;
		HEA1 B 5 A_Look();
		HEA1 A 5;
		loop;
	See:
		HEA1 AAAAAAAAAA 3 A_Chase();
		HEA1 BCBA 3 A_Chase();
		loop;
	Missile:
		HEAA ABCCDD 1 A_FaceTarget();
		HEAA E 4 A_CustomComboAttack("BD_CacodemonBall", 32, 10 * random(1, 6));
		HEAA CCA 1;
		Goto See;
	Pain:
		HEAA F 3;
		HEAA G 3 A_Pain();
		HEAA HGF 2;
		Goto See;
	Crush:
		CRS2 B -1;
		stop;
	Death:
		TNT1 A 0 {
			scale.x*=randompick(-1,1);
			bFORCEXYBILLBOARD = false;
			if (burning)
				return ResolveState("Death.Vanilla");
			return A_jump(256,"Death.Vanilla","Death1", "Death2");
			return ResolveState(null);	
			}
	Death.Vanilla:	
		HEAM A 3 BD_Bleed();
		HEAM B 3 A_Scream();
		HEAM CD 3 BD_Bleed();
		HEAM E 3 A_NoBlocking();
		HEAM F 3 A_SetFloorClip();
		HEAM GHI 3;
		goto deathbleed;
	Death1: //no eye, intestines fall down
		HEAN AB 3;
		TNT1 A 0 A_Scream();
		HEAN C 3 {
			if (bdoom_gibs && !burning)
				A_SpawnItemEx("CacodemonEye",frandom(-2,2),frandom(-2,2),frandom(28,32),frandom(2,5),0,frandom(0,5),frandom(-10,10),SXF_SETTARGET);
			}
		 TNT1 A 0 BD_Bleed();
		HEAN DE 3;
		HEAN F 3 A_NoBlocking();
		 TNT1 A 0 BD_Bleed();
		HEAN GHIJKL 3;
		TNT1 A 0 A_SetFloorClip();
		HEAN MNO 5;
		goto deathbleed;
	Death2: //puke gibs, less common death
		TNT1 A 0 {
			if (!bdoom_gibs || random(1,3) == 3)
				return ResolveState ("Death");
			bNOGRAVITY = true; 
			return ResolveState (null);
			}
		HEAP ABC 4;
		TNT1 A 0 {
			if (bdoom_gibs)
				A_SpawnItemEx("CacodemonEye",frandom(-2,2),frandom(-2,2),frandom(28,32),frandom(2,5),0,frandom(0,5),frandom(-10,10),SXF_SETTARGET);
			}
		HEAP CCDDEEFF 2 {
			if (bdoom_gibs)
				A_SpawnItemEx("CacodemonMeatPiece",0,frandom(-6,6),frandom(8,20),	frandom(3,5),0,frandom(2,3),	random(-15,15),	SXF_SETTARGET,48);
			}
		TNT1 A 0 {
			A_NoBlocking();
			bNOGRAVITY = false;
			}
		HEAP GJ 4;
		HEAP I 4 A_SetFloorClip();
		HEAP JKL 4;
		goto deathbleed;
	Death.Saw: //sliced diagonally
		TNT1 A 0 A_Jump(128,"Death");
		HEAM A 3 BD_Bleed();
		HEAM B 3 A_Scream();
		TNT1 A 0 A_FaceTarget();
		HEAO AABBCCDD 2 {
			if (bdoom_blood)
				A_SpawnItemEx("BloodSpurt",	1,0,frandom(30,38),0,0,0,random(-40,40),SXF_SETTARGET,96);
			}
		TNT1 A 0 {
			A_NoBlocking();
			A_SetFloorClip();
			}
		HEAO EFG 4;
		goto deathbleed;
	XDeath: //sprite gibbing from Smooth Doom with added particles
		TNT1 A 0 A_Jump(128,"Death");
		HEAX ABC 3 {
			if (bdoom_gibs)
				A_SpawnItemEx("CacodemonMeatPiece",frandom(-6,6),frandom(-6,6),frandom(8,20),	frandom(-6,6),frandom(-6,6),frandom(4,6), 0,	SXF_SETTARGET,48);
			}
		TNT1 A 0 A_XScream();
		HEAX DEF 3 {
			if (bdoom_gibs)
				A_SpawnItemEx("CacodemonMeatPiece",frandom(-6,6),frandom(-6,6),frandom(8,20),	frandom(-6,6),frandom(-6,6),frandom(4,6), 0,	SXF_SETTARGET,48);
			}
		TNT1 A 0 {
			A_NoBlocking();
			A_SetFloorClip();
			}
		HEAX GH 2 {
			if (bdoom_gibs)
				A_SpawnItemEx("CacodemonMeatPiece",frandom(-6,6),frandom(-6,6),frandom(8,20),	frandom(-6,6),frandom(-6,6),frandom(4,6), 0,	SXF_SETTARGET,48);
			}
		HEAX IJKLMNOPQRS 2;
		goto deathbleed;
	}
}

Class CacoRedTrail : Actor
{
Default {
	+NOINTERACTION
	+NOBLOCKMAP
	renderstyle "Add";
	alpha 0.8;
	scale 0.05;
	}
states
	{
	Spawn:
		LENR A 1 {
			A_FadeOut(0.2);
			scale*=0.95;
			}
		loop;
	}
}

Class CacoBlueTrail : Actor
{
Default {
	+NOINTERACTION
	+NOBLOCKMAP
	renderstyle "Add";
	alpha 0.8;
	scale 0.1;
	}
states
	{
	Spawn:
		LENB A 1 {
			A_FadeOut(0.2);
			scale*=0.95;
			}
		loop;
	}
}

Class BD_CacodemonBall : CacodemonBall //replaces CacodemonBall
{
int broll;
Default {
	+FORCEXYBILLBOARD
	+ROLLSPRITE
	-ROLLCENTER
	decal "Scorch";
	renderstyle "Add";
	alpha 1.0;
	scale 0.2;
	}
override void PostBeginPlay () {
	super.PostBeginPlay();
	scale.x*= randompick(-1,1);
	scale.y*= randompick(-1,1);
	roll = random(0,360);
	broll = random(6,8)*randompick(1,-1);
	if (bdoom_debris) {
		let f = BD_ProjFlare(Spawn("BD_ProjFlare",pos));
		f.fcolor = 'blue';
		f.scale = (0.21,0.21);
		f.alpha = 0.3;
		f.master = self;
		
		/*let f1 = BD_ProjFlare(Spawn("BD_ProjFlare",pos));
		f1.fcolor = 'red';
		f1.scale = (0.1,0.1);
		f1.alpha = 0.85;
		f1.master = self;*/
		}
	}	
override void Tick () {
    Vector3 oldPos = self.pos;    
    Super.Tick();
	if (!bdoom_debris || level.isFrozen() )
		return;
	Vector3 path = level.vec3Diff( self.pos, oldPos );
	double distance = path.length() / 3; //this determines how far apart the particles are
	Vector3 direction = path / distance;
	int steps = int( distance );
	
	for( int i = 0; i < steps; i++ )  {
		Spawn("CacoRedTrail", oldPos );
		Spawn("CacoBlueTrail", oldPos );
		oldPos = level.vec3Offset( oldPos, direction );
		}
	}
States
	{
	Spawn:
		ELBA ABCDEFGHIJKLMNO 2 bright NoDelay {
			roll+=broll;
			/*if (bdoom_debris) {
				A_SpawnItemEx("FirePiece_Cacodemon",frandom(-3,-6),frandom(-5,5),frandom(-5,5),0,0,0,failchance:96);
				A_SpawnItemEx("FirePiece_Cacodemon",frandom(-3,-6),frandom(-5,5),frandom(-5,5),0,0,0,failchance:96);
				}*/
			}
		loop;
	Death:
		TNT1 a 0 A_SetScale(1.0,1.0);
		CABX ABCDEF 3  A_FadeOut(0.1);
		stop;
	}
}

Class FirePiece_Cacodemon : Flare_General
{
Default {
	alpha 0.5;
	scale 0.1;
	}
states
	{
	Spawn:
		TNT1 A 2 NoDelay;
		SPRK C 1 bright  {
			A_FadeOut(0.04);
			scale*=0.9;
			}
		wait;
	}
}
