Class BD_PainElemental : BDoomMonster replaces PainElemental		
{
	Default
	{
	Health 400;
	Radius 31;
	Height 56;
	Mass 400;
	Speed 8;
	PainChance 128;
	Monster;
	+FLOAT 
	+NOGRAVITY
	SeeSound "pain/sight";
	PainSound "pain/pain";
	DeathSound "pain/death";
	ActiveSound "pain/active";
	Tag "$FN_PAIN";
	
	+FORCEXYBILLBOARD
	+FLOATBOB
	FloatBobStrength 0.35;
	BdoomMonster.allowfiredeath false;
	BdoomMonster.allowsmokedeath false;
	}
override void Tick() {
	super.Tick();
	if (!InStateSequence(curstate,FindState("Spawn")))
		bFLOATBOB = false;
	}
states
	{
	Spawn:
		PAI1 AAAAA 10 A_Look;
		PAI1 B 5 A_Look;
		PAI1 C 5;
		PAI1 D 5 A_Look;
		PAI1 B 5;
		Loop;
	see:
		
		SAIN abcdedcb 3 a_chase;
		Loop;
	Missile:
		PATT abc 3 a_facetarget;
		PATT d 3 a_facetarget;
		PATT e 2 a_facetarget;
		PATT e 1 a_painattack;
		Goto See;
	Pain:
		
		PPPP a 4;
		PPPP b 2;
		PPPP b 2 a_pain;
		PPPP c 4 a_pain;
		Goto See;
	death:
		TNT1 A 0 {
			scale.x *= randompick(-1,1); 
			gravity = 0.1;
			}
		SPAI AB 4;
		spai c 4 a_scream;
		spai defg 4;
		TNT1 A 0 {
			A_SetRenderstyle(0.65,Style_ADD);
			if (bdoom_gibs) {
				for (int i = 16; i > 0; i--)
					A_SpawnItemEx("BFountainSpray",frandom(-5.0,5.0),frandom(-5.0,5.0),random(8,28),vel.x*frandom(0.05,0.15),vel.y*frandom(0.05,0.15),frandom(4.5,6.5),0,SXF_ABSOLUTEVELOCITY | SXF_SETTARGET | SXF_TRANSFERTRANSLATION,36);
				A_SpawnItemEx("PainElementalHorn",	frandom(-4,4),frandom(-16,-18),frandom(32,36),	frandom(3,5),0,frandom(4,7),frandom(85,95),		SXF_SETTARGET,32);
				A_SpawnItemEx("PainElementalHorn",	frandom(-4,4),frandom(16,18),frandom(32,36),	frandom(3,5),0,frandom(4,7),frandom(-85,-95),	SXF_SETTARGET,32);
				A_SpawnItemEx("PainElementalJaw",	frandom(-4,4),frandom(-4,4),frandom(6,12),		frandom(-5,5),frandom(-5,5),frandom(3,5),0, 	SXF_SETTARGET,64);
				A_SpawnItemEx("PainElementalJaw",	frandom(-4,4),frandom(-4,4),frandom(6,12),		frandom(-5,5),frandom(-5,5),frandom(3,5),0, 	SXF_SETTARGET,64);
				A_SpawnItemEx("PainElementalHand",	frandom(-4,4),frandom(-20,-22),frandom(20,22),	frandom(3,4),0,frandom(4,7),frandom(85,95),		SXF_SETTARGET,32);
				A_SpawnItemEx("PainElementalHand",	frandom(-4,4),frandom(20,22),frandom(20,22),	frandom(3,4),0,frandom(4,7),frandom(-85,-95),	SXF_SETTARGET,32);
				for (int i = 0; i < 8; i++)
					A_SpawnItemEx("BasicMeatPiece",	frandom(-10,10),frandom(-10,10),frandom(6,40),	frandom(-5,5),frandom(-5,5),frandom(3,6),0, 	SXF_SETTARGET,32);
				for (int i = 0; i < 5; i++)
					A_SpawnItemEx("PEFlameDebris",	frandom(-6,6),frandom(-6,6),frandom(10,38),		frandom(-7,7),frandom(-7,7),frandom(2,6),0, 	SXF_SETTARGET,32);
				}
			}
		SPAI H 3 bright;
		spai i 3 bright a_paindie;
		spai jk 3 bright;
		stop;
	Crush:
		CRS2 A -1;
		stop;
	Raise:
		stop;
	XDeath:
		stop;
		}
}

Class PEFlameDebris : DebrisGeneral 
{
double fades;
Default {
	-NOGRAVITY
	DebrisGeneral.sfxtype 3;
	renderstyle "Add";
	alpha 0.8;
	gravity 0.6;
	scale 0.25;
	}
override void PostBeginPlay() {
	super.PostBeginPlay();
	fades = frandom(0.009,0.014);
	}
states
	{
	Spawn:
		FIRP AABBCCBB 1 bright NoDelay {
			A_SpawnItemEx("FirePiece",0,frandom(-3,3),frandom(-3,3),0,0,frandom(0.2,0.3));
			roll+=10;
			}
		loop;
	Death:
		FIRP AABBCCBB 1 bright NoDelay {
			A_SpawnItemEx("ParticleFire_PE",frandom(-3,3),frandom(-3,3),frandom(1,4),frandom(-0.2,0.2),frandom(-0.2,0.2),frandom(0.4,0.6),0,0,128);
			A_FadeOut(fades);
			}
		loop;
	}
}



Class ParticleFire_PE : ParticleFire_Red
{
Default {
	scale 0.14;
	}
}