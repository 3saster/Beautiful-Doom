Class BDoomWeapon : DoomWeapon
{
override void DoEffect()
	{
		if (owner)
			{
			if(bdoom_weapons == 2)
				{
					BobRangeX = 0.2;
					BobRangeY = 0.2;
					BobStyle = 5;
					BobSpeed = 2.0;
				}
			else
				{
					BobRangeX = 1.0;
					BobRangeY = 1.0;
					BobStyle = 0;
					BobSpeed = 1.0;
				}
			}
		Super.DoEffect();
	}

action void BD_WeaponReady(bool allowsecondary = false)
	{
	if (invoker.ammo2 && invoker.ammo2.amount > 0 && invoker.ammo1.amount < invoker.ammo1.maxamount) 
		{
		if (allowsecondary)	
			A_WeaponReady(WRF_ALLOWRELOAD);
		else
			A_WeaponReady(WRF_NOSECONDARY|WRF_ALLOWRELOAD);
		}
	else 
		{
		if (allowsecondary)	
			A_WeaponReady();
		else
			A_WeaponReady(WRF_NOSECONDARY);
		}
	
	}

action void BD_Reload()
	{
	if (invoker.ammo2)
		{
		while (invoker.ammo2.amount > 0 && invoker.ammo1.amount < invoker.ammo1.maxamount)
			{
			TakeInventory(invoker.ammo2.GetClass(),1);
			GiveInventory(invoker.ammo1.GetClass(),1);
			}
		}
	}
}


//////////////

Class BulletTracer : FastProjectile
{
Default {
	-ACTIVATEIMPACT;
	-ACTIVATEPCROSS;
	+NOTELEPORT;
	+BLOODLESSIMPACT;
	+THRUGHOST;
	alpha 0.75;
	renderstyle "add";
	damage 0;
	speed 150;
	radius 4;
	height 4;
	seesound "null";
	deathsound "null";
	}    
override void Tick () {
        Super.Tick ();

        if (!playeringame [consolePlayer])
            return;
        
        let curCamera = players [consolePlayer].camera;
        if (!curCamera) // If the player's "camera" variable is null, set it to their PlayerPawn
            curCamera = players [consolePlayer].mo;
        if (!curCamera) // If the player's PlayerPawn is null too, just stop trying
            return;

        if (CheckIfCloser (curCamera, 192))
            A_PlaySound("weapons/tracerwhizz",1,1.0,0,8);
    }
states
	{
	Spawn:
		PUFF A 1 bright {
			if (bdoom_tracers == 0)
				self.destroy();
			}
		loop;
	Xdeath:
		TNT1 A 1;
		stop;
	Death:
		TNT1 A 1 {
			if (frandom(0.0,1.0) > 0.8)
				A_SpawnProjectile("RicochetBullet",0,0,random(0,360),2,random(-40,40));
			}
		stop;
	}
}

Class RealBullet : BulletTracer
{
Default {
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	+NOTELEPORT
	-BLOODLESSIMPACT
	+BLOODSPLATTER
	+THRUGHOST
	decal "BulletChip";
	speed 150;
	radius 1;
	height 1;
	}
states
	{
	Death:
		TNT1 A 1 {
			A_SpawnItem("EnBulletPuff",0,0,0,0);
			if (frandom(0.0,1.0) > 0.8)
				A_SpawnProjectile("RicochetBullet",0,0,random(0,360),2,random(-40,40));
			}
		stop;
	}
}

Class BD_PistolBullet : RealBullet
{
Default {
	DamageFunction (3 * random(4,8));
	}
}

Class BD_ShotgunPellet : RealBullet
{
Default {
	DamageFunction (3 * random(2,5));
	decal "ShotChip";
	}
}


Class BD_RifleBullet : BD_PistolBullet
{
Default {
	DamageFunction (3 * frandom(6.5,8.0));
	}
}

/////////////////////
//// BULLET PUFF ////
/////////////////////

Class NullPuff : BulletPuff
{
Default {
	+NOINTERACTION
	+BLOODLESSIMPACT
	}
states
	{
		Spawn:
			TNT1 A 1 Nodelay;
			stop;
	}
}

Class EnBulletPuff : Actor
{
Default {
	+NOGRAVITY
	+FORCEXYBILLBOARD
	-DONTSPLASH
	-ALLOWPARTICLES
	decal "bulletchip";
	renderstyle "Add";
	scale 0.1;
	alpha 0.8;
	mass 1;
	}
states
	{
	Spawn:
		TNT1 A 1 NoDelay {
			if (bdoom_debris == 0) {
				A_SpawnItem("ShotSmoke");
				for (int i = 0; i < 6; i++)
					A_SpawnItemEx("Wallpart",0,0,0,	random(1,5),0,random(2,6),	random(0,360),0,64);
				}
			}
		BPUF ABCD 1 bright;
		stop;
	}
}


Class EnShotPuff : EnBulletPuff
{
Default {
	-NOEXTREMEDEATH
	decal "ShotChip";
	}
}

Class WallPart : DebrisGeneral
{
Default {
	bouncetype "Doom";
	-NOGRAVITY
	+DONTSPLASH
	speed 8;
	bouncefactor 0.4;
	Renderstyle "Translucent";
	alpha 1.0;
	scale 0.1;
	translation "0:4=5:8","5:8=5:8","9:12=5:8","13:15=5:8","16:47=5:8","48:79=5:8","80:111=5:8","112:127=5:8","128:151=5:8","152:159=5:8","160:167=5:8","168:191=5:8","192:207=5:8","208:223=5:8","224:231=5:8","232:235=5:8","236:239=5:8","240:247=5:8","248:249=5:8","250:254=5:8","255:255=5:8";
	}
states
	{
	Spawn:
		BPRT ACDEFGH 2 {
			if (vel.z == 0)
				self.destroy();
			}
		BPRT ACDEFGH 3 {
			if (vel.z == 0)
				self.destroy();
			}
		stop;
	 Death:
		TNT1 A 1;
	 	stop;
	 }
}

Class Spark_Spawner : DebrisGeneral
{
Default {
	+NOINTERACTION
	+NOGRAVITY
	}
states
	{
	Spawn:
		TNT1 AAAAAAAAAAAAAAA 0 NoDelay A_SpawnItemEx("RicochetSpark",0,0,0,	random(1,2),0,random(5,9),random(0,360),0,40);
		TNT1 A 1;
		stop;
	}
}

Class RicochetSpark : DebrisGeneral
{
Default {
	bouncetype "None";
	-NOGRAVITY
	+DONTSPLASH
	renderstyle "ADD";
	alpha 1.0;
	radius 3;
	height 3;
	scale 0.03;
	gravity 1.0;
	}
states
	{
	Spawn:
		SPRK A 1 bright A_FadeOut(0.05);
		loop;
	}
}

Class RicochetBullet : Actor
{
Default {
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	+FORCEXYBILLBOARD
	+THRUACTORS
	bouncetype "Hexen";
	mass 1;
	damage 0;
	radius 1;
	height 1;
	speed 30;
	seesound "none";
	renderstyle "Add";
	alpha 0.8;
	}
states
	{
	Spawn:
		TNT1 A 0 NoDelay {
			if (bdoom_sparks == 1)
				A_PlaySound("weapons/ricochet");
			}
		PUFF A 2;
		stop;
	}
}