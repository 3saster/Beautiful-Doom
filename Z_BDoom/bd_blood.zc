Class BD_BloodBase : BD_BaseDebris abstract {
	static const name bsprite[] = { "YBL7","YBL8","YBL9" };
	Default {
		BD_BaseDebris.sfxtype 'blood';
		+PUFFGETSOWNER
		-NOGRAVITY
		+DONTSPLASH
		gravity 0.7;
		renderstyle "Translucent";
		seesound "none";
		Deathsound "none";
		decal "none";
		height 1;
	}
	states	{
		LoadSprites:
			YBL7 ABCDEFGHIJKLMNOPQRS 0;
			YBL8 ABCDEFGHIJKLMNOPQRS 0;
			YBL9 ABCDEFGHIJKLMNOPQRS 0;
			XBL7 ABCDEFGHIJKLMNOPQRS 0;
			XBL8 ABCDEFGHIJKLMNOPQRS 0;
			XBL9 ABCDEFGHIJKLMNOPQRS 0;
			stop;
		Fade:
			#### # 1 A_FadeOut(0.02);
			loop;
	}
}

Class BD_BaseBloodPool : SmallDebris abstract {
	protected double sectorz;
	protected bool RandomAngle;
	property RandomAngle : RandomAngle;
	Default {
		BD_BaseBloodPool.RandomAngle true;
		BD_BaseDebris.sfxtype "blood";
		SmallDebris.removeonfall false;
		SmallDebris.dbrake 0;
		gravity 0;
	}
	override void PostBeginPlay() {
		super.PostBeginPlay();
		if (floorpic == skyflatnum){
			destroy();
			return;
		}
		if (!sectorz)
			sectorz = floorz+frandom(0.1,0.3);
		if (RandomAngle)
			angle = random(0,360);
		if (CurSector.FloorPlane.Normal != (0,0,1))	
			AlignToPlane(self);
	}
	override void Tick() {
		super.Tick();
		SetOrigin((pos.x,pos.y,sectorz),true);
		if (isFrozen())
			return;
		if (BDoomActor.CheckWater(self)) {
			A_SetRenderstyle(alpha,STYLE_Translucent);
			A_FadeOut(0.01);
		}
	}
}
	

Class BD_Blood : BD_BloodBase {
	double restscale;
	protected bool onceiling;
	Default {
		scale 0.35;
		gravity 0.5;
		renderstyle "Translucent";
		alpha 0.8;
		height 2;
		radius 4;
		deathsound "";
		decal "";
	}
	override void BeginPlay() {
		super.BeginPlay();
		if (target) {
			CopyBloodColor(target);
			translation = target.BloodTranslation;
		}
	}
	override void PostBeginPlay() {
		if (!bdoom_blood /*|| !target*/){
			destroy();
			return;
		}
		if (target && (target is "BDoomMonster")) {
			CopyBloodColor(target);
			translation = target.BloodTranslation;
			let trg = BDoomMonster(target);
			if (trg) {
				if (trg.burning){
					destroy();
					return;
				}
				trg.meatlist.push(self);
			}
		}
		Super.PostBeginPlay();
	}
	states {
		spawn:
			TNT1 AAA 0; //override hardcoded frame-skipping behavior for Blood
			TNT1 A 0 {
				A_SetScale(scale.x*frandom(0.7,1.2),scale.y*frandom(0.7,1.2));
				bSPRITEFLIP = randompick(0,1);
				roll = random(0,359);
				sprite = GetSpriteIndex(bsprite[random(0,2)]);
				return ResolveState("BloodDo");
			}
		BloodDo:
			#### ABCDEFGHIJKLMNOPQRS 1 {
				A_FaceMovementDirection();
				if (waterlevel > 0)
					return ResolveState("Water");
				A_SetRenderStyle(alpha,Style_Translucent);				
				return ResolveState(null);
			}
			stop;
		Death:
			MODL A 0 { 
				A_Stop();
				A_Scream();
				if (blockingline) {
					int dmg = 1;
					if (target)
						dmg = target.GetMaxHealth() - target.health;
					TraceBleedAngle(dmg,angle,pitch);
					destroy();
					return ResolveState(null);
				}
				frame = random(0,12);
				bNOINTERACTION = true;
				A_ChangeLinkFlags(blockmap:true);
				angle = random(0,359);
				if (pos.z >= ceilingz-10) {
					if (ceilingpic == skyflatnum) {
						destroy();
						return ResolveState(null);
					}
					AlignToPlane(self,ceiling:true);
					if (CurSector.CeilingPlane.Normal == (0,0,-1)) {
						if (CheckLandingSize(24,true))
							scale*=0.5;
						if (CheckLandingSize(18,true))
							scale*=0.5;
						if (CheckLandingSize(12,true))
							scale*=0.5;
						if (CheckLandingSize(6,true)){
							destroy();
							return ResolveState(null);
						}
					}
					onceiling = true;
					SetOrigin((pos.x,pos.y,ceilingz-1),true);
					A_SetRenderStyle(0.8,Style_Translucent);
					return ResolveState("OnCeiling");
				}	
				else {
					if (floorpic == skyflatnum) { 
						destroy();
						return ResolveState(null);
					}
					AlignToPlane(self);
					if (CurSector.FloorPlane.Normal == (0,0,1)) {
						if (CheckLandingSize(24))
							scale*=0.5;
						if (CheckLandingSize(18))
							scale*=0.5;
						if (CheckLandingSize(12))
							scale*=0.5;
						if (CheckLandingSize(6)) {
							destroy();
							return ResolveState(null);
						}
					}
				}
				bMOVEWITHSECTOR = true;
				bRELATIVETOFLOOR = true;
				SetOrigin((pos.x,pos.y,floorz+frandom(0.2,0.5)),false);
				A_SetRenderStyle(1,STYLE_Normal);
				if (BDoomActor.CheckWater(self))
					return ResolveState("Water");
				return ResolveState(null);
			}
			//MODL ABCDEFGHIJKLMNOPQRS 20;
			#### # -1;
			stop;
		OnCeiling:
			#### ###### 1 SetOrigin((pos.x,pos.y,ceilingz-0.3),true);
			#### # 0 {
				int chance = Clamp (320 - (128 * alpha),64,250);
				A_SpawnitemEx("BD_CeilingBloodDrop",frandom(-3,3),frandom(-3,3),-1,flags:SXF_TRANSFERTRANSLATION|SXF_NOPOINTERS,chance);
				A_FadeOut(0.001);
			}
			loop;			
		Raise:
			#### # 0 {
				if (onceiling)
					return ResolveState("Fade");
				sprite = GetSpriteIndex(bsprite[random(0,2)]);
				roll = random(0,359);
				FlyBack();
				return ResolveState(null);
			}
			#### SRQPONMLKJIHGFEDCBA 1 bright {
				A_FadeOut(0.03);
				if (target &&  Distance3D(target) < 32) {
					destroy();
					return ResolveState(null);
				}
				return ResolveState(null);
			}
			stop;
		FadeOut:
			#### # 0 A_SetRenderstyle(alpha,Style_Translucent);
			#### # 1 A_FadeOut(0.1);
			wait;
		Water:
			#### # 0 {
				A_SetRenderStyle(alpha,STYLE_Translucent);
				//console.printf("blood in water");
			}
			#### # 1 {
				A_FadeOut(0.05);
				scale*=1.01;
			}
			wait;
	}
}

Class BD_BloodPool : BD_BaseBloodPool {
	Default {
		scale 0.35;
	}
	override void PostBeginPlay() {
		super.PostBeginPlay();
		if (CurSector.FloorPlane.Normal == (0,0,1)) {
			if (CheckLandingSize(24))
				scale*=0.5;
			if	(CheckLandingSize(18))
				scale*=0.5;
			if	(CheckLandingSize(12))
				scale*=0.5;
			if	(CheckLandingSize(6)){
				destroy();
				return;
			}
		}
		frame = random(0,12); 
	}
	states {
		spawn:
			MODL # -1;
			stop;
	}
}

Class BD_CeilingBloodDrop : SmallDebris {
	Default {
		BD_BaseDebris.sfxtype 'blood';
		+FORCEYBILLBOARD
		scale 0.1;
		alpha 0.75;
		gravity 0.05;
		SmallDebris.removeonfall false;
		SmallDebris.dbrake 0;
	}
	override void PostBeginPlay() {
		super.PostBeginPlay();
		scale*= frandom(0.8,1.1);
		bSPRITEFLIP = randompick(0,1);
	}
	states {
		spawn:
			BLOD UVWWXXYYY 2;
			TNT1 A 0 { 
				gravity = 0.3; 
				moving = true;
			}
			BLOD ZZZZZZZZZZZZZZZZZZZZZZZZZ 1 {
				if (waterlevel > 0) {
					destroy();
					return ResolveState(null);
				}
				if (pos.z <= floorz)
					return ResolveState("Death");
				return ResolveState(null);
			}
			stop;
		Death:
			TNT1 A 0 {
				vel.z = 0.0;
				SetOrigin((pos.x,pos.y,floorz),0);
				scale.x = frandom(0.22,0.25);
				bSPRITEFLIP = randompick(0,1);
			}
			BLOD GHIJK 3;
			stop;
	}
}


Class AniBloodPool : BD_BaseBloodPool {
	bool instant; //if true, the blood pool will instantly reqch max size â€” for decorative purposes such as Heart Column, where animation is unneeded, but checking for overhang may still important
	protected double pscale;
	Default {
		renderstyle 'Normal';
		scale 0.05;
		radius 16;
	}
	override void PostBeginPlay() {
		Super.PostBeginPlay();
		if (waterlevel > 0) {
			destroy();
			return;
		}
		pscale = 1.017;
		if (instant)
			A_SetScale(0.208);
		if (CurSector.FloorPlane.Normal == (0,0,1)) {
			if (CheckLandingSize(18))
				scale*=0.5;
			if (CheckLandingSize(15))
				scale*=0.5;
			if (CheckLandingSize(9))
				scale*=0.5;
			if (CheckLandingSize(8)) {
				destroy();
				return;
			}
		}
		if (target)
			translation = Target.BloodTranslation;
	}
	override void Tick() {
		super.Tick();
		if (isfrozen() || instant)
			return;
		if (pscale > 1) {
			scale *= pscale;
			pscale = Clamp(pscale*0.9999,1,2);	
			Console.Printf("scale: %f",scale.x);
		}
	}
	states	{
		Spawn:
			MODL A -1;
			stop;
	}
}

//thinner and faster version for spurts
Class BloodSpray : BD_Blood {
	Default {
		scale 0.12;
		gravity 0.5;
		alpha 0.7;
	}
	override void Tick() {
		super.Tick();
		if (isFrozen())
			return;
		A_FadeOut(0.05);
	}
}

//long "arterial" spurts
Class BloodSpurt : BD_BloodBase {
	double bpitch;
	double pitchchange;
	double bspeed;
	string sprayactor;
	Default {
		+NOGRAVITY
		+NOINTERACTION
		radius 1;
		height 1;
	}
	override void PostBeginPlay() {
		if (waterlevel > 0) {
			destroy();
			return;
		}
		super.PostBeginPlay();
		bpitch = frandom(1.0,5.0);
		bspeed = frandom(4.0,6.0);
		pitchchange = 0.5;
		sprayactor = "BloodSpray";
	}
	states {
		Spawn:
			TNT1 A 0 NoDelay A_FaceTarget(0,270,0,0);
			TNT1 A 0 A_Jump(256,random(1,5));
			TNT1 AAAAAAAAA 1 { 
				A_SpawnItemEx(sprayactor,0,0,0,	bspeed+frandom(0.0,1.2),0,bpitch,	random(175,185), SXF_TRANSFERPOINTERS);
				bpitch+=pitchchange;
			}
			stop;
	}
}

Class NeckSpurt : BloodSpurt {
	override void PostBeginPlay() {
		super.PostBeginPlay();
		bpitch = frandom(2.0,5.5);
		bspeed = frandom(1.0,2.5);
		sprayactor = "NeckSpray";
		pitchchange = 0.0;
	}
}

Class FrontalSpurt : BloodSpurt {
	override void PostBeginPlay() {
		super.PostBeginPlay();
		bpitch = frandom(0.3,1.2);
		bspeed = frandom(3.0,4.5);
		sprayactor = "NeckSpray";
		pitchchange = 0.0;
	}
}

//shorter and smaller version of bloodspurt
Class NeckSpray : SmallDebris {
	Default {
		gravity 0.5;
		scale 0.12;
		alpha 0.7;
	}
	static const string bsprite[] =	{ "YBL7","YBL8","YBL9" };
	override void PostBeginPlay() {
		super.PostBeginPlay();
		bSPRITEFLIP = randompick(0,1);
		roll = random(0,359);
		sprite = GetSpriteIndex(bsprite[random(0,2)]);
	}
	states {
		spawn:
			#### ABCDEFGHIJKLMNOPQRS 2 {
				A_FadeOut(0.07);
				return ResolveState(null);
			}
			stop;
	}
}


//large quickly disappearing spray creating a "bloody mist" upon xdeath
Class BFountainSpray : SmallDebris {
	Default {
		BD_BaseDebris.sfxtype 'blood';
		+ROLLCENTER
		renderstyle "Translucent";
		scale 0.32;
		alpha 0.7;
	}
	override void PostBeginPlay() {
		super.PostBeginPlay();
		bSPRITEFLIP = randompick(0,1);
		roll = random(0,359);
	}
	states {
	Spawn:
		MISX ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 {
			scale *= 1.1;
			a_fadeout(0.05);
		}
		stop;
	}
}



//// FOOTPRINTS

Class BD_BloodyPrintControl : Inventory {
	FXAmountControl event;
	private int bloodtrack; //how much blood was "collected by our boots"
	private bool side;
	private bool drip;
	property drip : drip;
	private array <BD_BloodBase> bootsblood;
	Default {
		BD_BloodyPrintControl.drip false;
		+INVENTORY.UNDROPPABLE
		+INVENTORY.UNTOSSABLE
		inventory.maxamount 1;
	}
	static const name LiquidFloors[] = { 
		"BLOOD1", "BLOOD2", "BLOOD3", "SLIME01", "SLIME02", "SLIME03", "SLIME04", "SLIME05", "SLIME06", "SLIME07", "SLIME08", "SLIME09", "SLIME10", "SLIME11", "SLIME12"
	};
	override void Tick() {}
	override void DoEffect() {
		super.DoEffect();		
		if (!owner || !bdoom_blood || !owner.player || level.isFrozen())
			return;
		//debug
		//Console.Printf("blood on boots: %d | vel: %d",bootsblood.Size(),owner.vel.length());
		event = FXAmountControl(EventHandler.find("FXAmountControl"));
		if (!event)
			return;
		//blood isn't collected on boots and prints aren't spawned if we're not on the floor or are barely moving
		if (!(owner.pos.z ~== owner.floorz))
			return;
		if (BDoomActor.CheckWater(owner)) {
			bootsblood.Clear();
			return;
		}
		double pspeed = owner.vel.length();
		if (drip && (level.time % 6 == 0))
			bootsblood.delete(0);
		if (bootsblood.Size() < 48 && pspeed > 4) {
			for (int i = 0; i < event.bloodlist.Size(); i++) {
				if (bootsblood.Size() >= 48)
					break;
				let pool = BD_BloodBase(event.bloodlist[i]);
				if (!pool || !pool.bNOINTERACTION || /*!(pool.pos.z ~== pool.floorz) ||*/ bootsblood.Find (pool) != bootsBlood.Size ())
					continue;
				if (pool is "BD_Blood" || pool is "AniBloodPool") {
					/*let diff = owner.Vec3To (pool);
					double combinedRadius = owner.radius + pool.radius;
					if ((diff.XY dot diff.XY) < (combinedRadius * combinedRadius))*/
					let diff = owner.Distance2D(pool);
					if (pool.floorz == owner.floorz && diff <= owner.radius)
						bootsblood.Push(pool);
				}
			}
		}
		if (bootsblood.Size() > 12 && (level.time % 6 == 0) && pspeed >= 2) {
			drip = true;
			let print = BD_BloodyFootPrint(Spawn("BD_BloodyFootPrint",(owner.pos.x,owner.pos.y,owner.floorz)));
			if (print) {
				print.side = side;
				print.angle = owner.angle;
				print.translation = bootsblood[bootsBlood.Size()-1].BloodTranslation;
				side = !side;
			}
		}
		else if (bootsblood.Size() == 0)
			drip = false;
	}
}

Class BD_BloodyFootPrint : BD_BaseBloodPool {
	bool side;
	Default {
		BD_BaseBloodPool.RandomAngle false;
		renderstyle 'Translucent';
		alpha 1.0;
		radius 4;
		scale 1;
	}
	override void Tick() {
		super.Tick();
		if (!isFrozen())
			A_FadeOut(0.001);
	}
	states {
		Spawn:
			0000 A -1 NoDelay {
				if (side)
					frame = 1;				
			}
			stop;
	}
}