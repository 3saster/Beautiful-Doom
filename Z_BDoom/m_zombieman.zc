Class BD_ZombieMan : BDoomMonster replaces Zombieman
{
Default {
		Health 20;
		Radius 20;
		Height 56;
		Speed 8;
		PainChance 200;
		SeeSound "grunt/sight";
		PainSound "grunt/pain";
		DeathSound "grunt/death";
		ActiveSound "grunt/active";
		Obituary "$OB_ZOMBIE";
		Tag "$FN_ZOMBIE";
	BdoomMonster.monsterclip 15;
	+DOHARMSPECIES
	dropitem "BD_Pistol",40,1;
	dropitem "Clip",256;
	}
	
states
	{
	Spawn:
		PSSS AB 10 A_Look();
		loop;
	See:
		TNT1 A 0 A_SetScale(1.0,1.0);
	    POS1 ABCDDCBA 4 A_Chase();
		loop; 	
	Crush:
		CRS1 A -1;
		stop;
	Missile:
		TNT1 A 0 A_Jumpif(bdoom_weapons==2,"MissileModern");
		PSSA A 5 A_FaceTarget();
		PSSA B 2 A_FaceTarget();
		POS1 E 3 A_FaceTarget();
		POS1 F 3 {
			if (bdoom_debris == 1)
				A_SpawnItemEx("PistolCasing",frandom(4,6),-22,frandom(37,38),		frandom(1,2),0,frandom(4,6),	random(-85,-95),SXF_SETTARGET);
			If(bdoom_weapons == 1)
				A_PlaySound("weapons/pistol/fire");
			else			
				A_PlaySound("grunt/attack");
			if (bdoom_tracers == 1)
				A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "EnBulletPuff", 0, CBAF_NORANDOM,AAPTR_TARGET,"BulletTracer",29,45);
			else
				A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "EnBulletPuff", 0, CBAF_NORANDOM);
			}
		POS1 E 3;
		PSSA BA 4;
		PSSA A 2;
		goto See;
	MissileModern:
		PSSA A 5 A_FaceTarget();
		PSSA B 2 A_FaceTarget();
		POS1 E 3 A_FaceTarget();
		POS1 F 3 {
			if (bdoom_debris == 1)
				A_SpawnItemEx("PistolCasing",frandom(4,6),-22,frandom(37,38),		frandom(1,2),0,frandom(4,6),	random(-85,-95),SXF_SETTARGET);
			A_PlaySound("Mweapons/pistol/fire");
			A_CustomBulletAttack(17.0,1.3,1,0,"NullPuff",0,0,AAPTR_TARGET,"BD_ZombieBullet",29,45);
			monsterclip--;
			}
		POS1 E 14;
		TNT1 A 0 { 
			if (monsterclip == 0)
				return ResolveState("Reload");
			else
				return A_MonsterRefire(80,"MissileModernEnd");
			return ResolveState(null);
			}
		goto MissileModern+3;
	MissileModernEnd:
		PSSA BA 4;
		PSSA A 2;
		goto See;
	Reload:
		TNT1 A 0 {
			bFRIGHTENED = true;
			A_PlaySound("Mweapons/pistol/clipout");
			monsterclip = 15;
			}
	    POS1 ABCDDCBAABCDDCBA 4 A_Chase(null,null);
		TNT1 A 0 A_PlaySound("Mweapons/pistol/clipin");
	    POS1 ABCDDCBA 4 A_Chase(null,null);
		TNT1 A 0 { bFRIGHTENED = false; }
		goto see;
	Pain:
		stro a 3;
		stro b 3 A_Pain();
		goto see;
	Death:
		TNT1 A 0 {
			if (bdoom_gibs == 1 && health < -10 && !burning)
				return ResolveState("DecapDeath");
			else
				return A_Jump(256,"Death.Vanilla","Death1","Death2");
			return ResolveState(null);
			}
	Death.Vanilla:
		POSM A 3 {
			A_Scream();
			BD_Bleed();
			}
		POSM B 3 {
			A_NoBlocking();
			BD_Bleed();
			}
		POSM CDEFG 3;
		POSM H 3;
		POSM I 1;
		goto deathbleed;
	Death1: //fall forward
		POSO A 3 BD_Bleed();
		POSO B 3 A_Scream();
		POSO C 3 {
			A_NoBlocking();
			BD_Bleed();
			}
		POSO D 3 BD_Bleed();
		POSO EFGHI 3;
		POSO JK 4;
		POSO L 1;
		goto deathbleed;
	Death2: //fall left/right
		TNT1 A 0 { 
			A_SetScale(scale.x*randompick(-1,1),scale.y);
			A_Scream();
			}
		POSN ABC 2 {
			if(!burning)
				A_SpawnItemEx("Blood",random(-2,2),0,random(32,34),	random(4,6),0,random(3,5),random(-70,-90)*scale.x,SXF_SETTARGET);
			}
		TNT1 A 0 A_NoBlocking();
		POSN DEF 3 {
			if(!burning)
				A_SpawnItemEx("Blood",random(-2,2),0,random(24,26),	random(4,6),0,random(3,5),random(-70,-90)*scale.x,SXF_SETTARGET);
			}
		POSN GHI 3 {
			if(!burning)
				A_SpawnItemEx("Blood",random(-2,2),0,random(10,16),	random(4,6),0,random(3,5),random(-70,-90)*scale.x,SXF_SETTARGET);
			}
		POSN KLM 3;
		POSN N 1;
		goto deathbleed;
	Death.Saw:
		TNT1 A 0 A_JumpIf(bdoom_gibs==0,"Death");
		//TNT1 A 0 A_Jump(128,"Death");
		TNT1 A 0 { 
			A_SetScale(scale.x*randompick(-1,1),scale.y);
			A_FaceTarget();
			A_Pain();
			}
		POSY AZ 18 	A_SpawnItemEx("BloodSprayer",	0,0,42,0,0,0,0,SXF_SETTARGET,144);
		POSY Z 5 {
			A_Pain();
			A_NoBlocking();
			}
		POSY AZ 20 	A_SpawnItemEx("BloodSprayer",	frandom(-4,4),random(-4,-4),random(24,26),0,0,0,0,SXF_SETTARGET,144);
		POSY Z 5 A_Scream();
		POSY BCDEFG 3 A_SpawnItemEx("Blood",	frandom(-4,4),frandom(0,6),	frandom(12,18),	frandom(2,4),0,frandom(4,6),	random(-20,20),SXF_SETTARGET,128);
		TNT1 A 0 A_PlaySound("gibs/meatland");
		POSY HI 2 		A_SpawnItemEx("Blood",	frandom(-4,4),frandom(0,6),	frandom(12,18),	frandom(2,4),0,frandom(4,6),	random(-20,20),SXF_SETTARGET,128);
		TNT1 A 0 A_PlaySound("gibs/meatland");
		POSY J 3 		A_SpawnItemEx("Blood",	frandom(-4,4),frandom(0,6),	frandom(12,18),	frandom(2,4),0,frandom(4,6),	random(-20,20),SXF_SETTARGET,128);
		POSY K 1;
		goto deathbleed;
	DecapDeath:
		POSD A 2 {
			A_SpawnItemEx("Gib_Head",0,0,48,	frandom(-3,-4),frandom(-1,-2),frandom(3,5),	0,SXF_SETTARGET);
			A_SpawnItemEx("Gib_Hand",0,14,24,	frandom(-2,2),frandom(3,4),frandom(2,3),	0,SXF_SETTARGET);
			}
		POSD BCDE 2 BD_Bleed();
		POSD FGH 2;
		goto deathbleed;
	XDeath:
		TNT1 A 0 A_JumpIf(bdoom_gibs==1,"XDeath.Particles");
	XDeath.Vanilla:
		ZGIB ab 2;
		ZGIB cd 2 A_XScream();
		ZGIB e 2 A_NoBlocking();
		ZGIB f 2;
		ZGIB ghijklmnopq 2;
		goto deathbleed;
	XDeath.Particles:
		TNT1 A 0 BD_Gibs();
		POSX AB 2;
		POSX C 2 A_XScream();
		POSX D 2 A_NoBlocking();
		POSX EFGHIJKLMNO 2;
		goto deathbleed;
		}
}

Class BD_ZombieBullet : RealBullet
{
Default {
	damageFunction (3 * random(1,5));
	}
}