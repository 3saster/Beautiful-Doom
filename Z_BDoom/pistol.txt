Class BD_Pistol : BDoomWeapon replaces Pistol
{
bool wasempty;
Default {
	Obituary "$OB_MPPISTOL";
	+WEAPON.WIMPY_WEAPON
	+WEAPON.NO_AUTO_SWITCH
	Inventory.Pickupmessage "Picked up a pistol";
	Inventory.Pickupsound "weapons/pistol/pickup";
	Tag "$TAG_PISTOL";
	weapon.selectionorder 190;
	inventory.icon "PISTZ0";
	weapon.slotnumber 2;
	weapon.ammotype1 		"Clip";
	weapon.ammouse1 		1;
	weapon.ammogive1	 	0;
	weapon.ammotype2 		"Clip";
	weapon.ammouse2 		1;
	weapon.ammogive2		20;
	inventory.amount		1;
	inventory.maxamount		2;
}
override void Tick()
	{
	if(bdoom_weapons == 1) 
		{ 
		A_SetRenderstyle (1.0,STYLE_Normal); 
		bSPECIAL = true;
		}
	else 
		{ 
		A_SetRenderstyle (1.0,STYLE_None); 
		bSPECIAL = false;
		}
	Super.Tick();
	}
override void DoEffect()
	{
	If (Owner)
		{
		if(bdoom_weapons == 2)
			{
				Ammo1 = ammo(Owner.FindInventory("PistolClip"));
				AmmoUse1 = 1;
				AmmoUse2 = 0;
				bNOAUTOFIRE = true;
				bAMMO_OPTIONAL = true;
			}
		else
			{
				Ammo1 = ammo(Owner.FindInventory("Clip"));
				AmmoUse1 = 1;
				AmmoUse2 = 1;
				bNOAUTOFIRE = false;
				bAMMO_OPTIONAL = false;
			}
		}
	Super.DoEffect();
	}
	
states
	{
	Spawn:
		PIST Z -1;
		stop;
	Ready:
		TNT1 A 0 A_JumpIf(bdoom_weapons==2,"Ready.Modern");
		TNT1 A 0 A_JumpIf(bdoom_weapons==1,"Ready.Enhanced");
		TNT1 A 0 A_Overlay(5,"none",0);
		PI0G A 1 A_WeaponReady(WRF_NOSECONDARY);
		Loop;
	Deselect:
		TNT1 A 0 A_Overlay(5,"None",0);
		TNT1 A 0 A_JumpIf(bdoom_weapons==2,"Deselect.Modern");
		TNT1 A 0 A_JumpIf(bdoom_weapons==1,"Deselect.Enhanced");
		PI0G A 1 A_Lower();
		wait;
	Select:
		TNT1 A 0 A_JumpIf(bdoom_weapons==2,"Select.Modern");
		TNT1 A 0 A_JumpIf(bdoom_weapons==1,"Select.Enhanced");
		TNT1 AAAAAAAA 0 A_Raise();
		PI0S ABCDEE 1 A_WeaponReady(WRF_NOFIRE);
		goto ready;
	Fire: //fire 14 tics, refire 5 tics;
		TNT1 A 0 A_JumpIf(bdoom_weapons==2,"Fire.Modern");
		TNT1 A 0 A_JumpIf(bdoom_weapons==1,"Fire.Enhanced");
		PI0F A 3 { 
			A_PlaySound("weapons/pistol");
			A_gunflash("Flash");
			If(GetCVAR("Bdoom_tracers")==1)
				A_FireBullets (5.6, 0, 1, 5, "EnBulletPuff",FBF_USEAMMO,0,"BulletTracer",0,0);
			else
				A_FireBullets (5.6, 0, 1, 5, "EnBulletPuff");
			}
		PI0G ABCD 1;
		TNT1 A 0 { 
			if( Bdoom_debris==1)
				A_SpawnItemEx("9mmCasing",random(3,4),cos(pitch)*-25,sin(-pitch)*25+random(31,32),		random(1,3),0,random(4,6),	random(-80,-90),0, SXF_ABSOLUTEMOMENTUM);
			}
		PI0G EF 2;
		PI0G A 3 ;
		TNT1 A 0 A_ReFire();
		PI0G A 5 { 
			If( Bdoom_debris==1)
				A_SpawnItemEx("WeaponSmoke",cos(pitch)*25,0,sin(-pitch)*25+random(33,36),		0,0,1,0);
			}
		Goto Ready;
	Flash:
		TNT1 A 0 A_JumpIf(bdoom_weapons==1,"Flash.Enhanced");
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_muzzle")==1,3);
		PI0F L 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		TNT1 A 0 A_Jump(256,1,3,5,7);
		PI0L A 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PI0L B 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PI0L C 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PI0L D 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
	Flash.Enhanced:
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_muzzle")==1,3);
		PIBF L 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		TNT1 A 0 A_Jump(256,1,3,5,7);
		PIBL A 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PIBL B 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PIBL C 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PIBL D 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;

	Ready.Enhanced:
		TNT1 A 0 A_JumpIfInventory("BD_Pistol",2,2);
		PIBN A 1 A_WeaponReady(WRF_NOSECONDARY);
		goto ready;
		PIBN A 1 { A_WeaponReady(); A_Overlay(5,"Overlay.Dual",0); }
		goto ready;
	Select.Enhanced:
		TNT1 A 0 A_JumpIfInventory("BD_Pistol",2,"Select.Dual");
		TNT1 A 0 A_PlaySound("weapons/pistol/draw");
		TNT1 AAAAAAAA 0 A_Raise();
		PIBS BCDE 1 A_WeaponReady(WRF_NOFIRE);
		goto ready;
	Select.Dual:
		TNT1 AAAAAAAA 0 A_Raise();
		TNT1 A 0 A_PLaysound("weapons/pistol/draw");
		PI2S ABCDE 1 A_WeaponReady(WRF_NOFIRE);
		goto ready;
	Deselect.Enhanced:
		TNT1 A 0 A_JumpIfInventory("BD_Pistol",2,"Deselect.Dual");
		TNT1 A 0 A_Lower();
		PIBN A 1 A_Lower();
		loop;
	Deselect.Dual:
		TNT1 A 0 A_Lower();
		PI2G A 1 A_Lower();
		loop;
	Overlay.Dual:
		PI2N A 20;
		stop;
	Fire.Enhanced:
		PIBF A 3 { if (CheckInventory("BD_Pistol",2,AAPTR_DEFAULT) == true) {
			A_Overlay(5,"Overlay.Dual",0); }
			A_PlaySound("weapons/pistol/fire");
			A_gunflash("Flash");
			If(GetCVAR("Bdoom_tracers")==1)
				A_FireBullets (5.6, 0, 1, 5, "EnBulletPuff",FBF_USEAMMO,0,"BulletTracer",0,0);
			else
				A_FireBullets (5.6, 0, 1, 5, "EnBulletPuff");
			}
		PIBN ABCD 1;
		TNT1 A 0 { If( Bdoom_debris==1)
			A_SpawnItemEx("9mmCasing",random(3,4),cos(pitch)*-25,sin(-pitch)*25+random(31,32),		random(1,3),0,random(4,6),	random(-80,-90),0, SXF_ABSOLUTEMOMENTUM);
			}
		PIBN EF 2;
		PIBN A 1 ;
		TNT1 A 0 A_ReFire("Fire.Enhanced");
		PIBN A 5 { If( Bdoom_debris==1)
			A_SpawnItemEx("WeaponSmoke",cos(pitch)*25,0,sin(-pitch)*25+random(33,36),		0,0,1,0);
			}
		goto ready;
	AltFire:
		PIBN A 2;
		TNT1 A 0 A_Overlay(5,"None",0);
		PIDC BCD 1;
		PIDF A 2 { 
			A_PlaySound("weapons/pistol/fire");
			A_gunflash("AltFlash.Right");
			If(GetCVAR("Bdoom_tracers")==1)
				A_FireBullets (5, 1.5, -1, 5, "EnBulletPuff",FBF_USEAMMO,0,"BulletTracer",0,7.5); 
			else
				 A_FireBullets (5, 1.5, -1, 5, "EnBulletPuff");
			}
		PIDF BC 1;
		PIDF D 2;
		TNT1 A 0 { If( Bdoom_debris==1)
			A_SpawnItemEx("9mmCasing",random(3,4),cos(pitch)*-25,sin(-pitch)*25+random(31,32),		random(1,3),0,random(4,6),	random(-80,-90),0, SXF_ABSOLUTEMOMENTUM);
			}
		TNT1 A 0 A_ReFire();
		PIDE E 2 { If( Bdoom_debris==1)
			A_SpawnItemEx("WeaponSmoke",cos(pitch)*25,0,sin(-pitch)*25+random(33,36),		0,0,1,0);
			}
		PIDF CB 1;
		PIDN A 1;
		PIDC DCB 2;
		goto ready;
	AltHold:
		PIDF E 2 { 
			A_PlaySound("weapons/pistol/fire");
			A_gunflash("AltFlash.Left");
			If(GetCVAR("Bdoom_tracers")==1)
				A_FireBullets (5, 1.5, -1, 5, "EnBulletPuff",FBF_USEAMMO,0,"BulletTracer",0,-7.5);
			else
				 A_FireBullets (5, 1.5, -1, 5, "EnBulletPuff");
			}
		PIDF FG 1;
		PIDF H 2;
		TNT1 A 0 A_CheckReload();
		PIDF I 2 { 
			A_PlaySound("weapons/pistol/fire");
			A_gunflash("AltFlash.Right");
			If(GetCVAR("Bdoom_tracers")==1)
				A_FireBullets (5, 1.5, -1, 5, "EnBulletPuff",FBF_USEAMMO,0,"BulletTracer",0,7.5);
			else
				 A_FireBullets (5, 1.5, -1, 5, "EnBulletPuff");
			}
		PIDF JK 1;
		PIDF L 2;
		TNT1 A 0 { If( Bdoom_debris==1)
			A_SpawnItemEx("9mmCasing",random(3,4),cos(pitch)*-25,sin(-pitch)*25+random(31,32),		random(1,3),0,random(4,6),	random(-80,-90),0, SXF_ABSOLUTEMOMENTUM);
			}
		TNT1 A 0 A_ReFire();
		PIDE E 2 { If( Bdoom_debris==1) {
			A_SpawnItemEx("WeaponSmoke",cos(pitch)*25,-6,sin(-pitch)*25+random(34,36),		0,0,1,0);
			A_SpawnItemEx("WeaponSmoke",cos(pitch)*25,6,sin(-pitch)*25+random(36,38),		0,0,1,0); 
				}
			}	
		PIDF CB 1;
		PIDN A 1;
		PIDC DCB 2;
		goto ready;
	AltFlash.Right:
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_muzzle")==1,3);
		PIDF O 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		TNT1 A 0 A_Jump(256,1,3,5,7);
		PIDL A 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PIDL B 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PIDL C 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PIDL D 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
	AltFlash.Left:
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_muzzle")==1,3);
		PIDF P 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		TNT1 A 0 A_Jump(256,1,3,5,7);
		PIDL E 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PIDL F 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PIDL G 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PIDL H 3 bright A_Light1();
		TNT1 A 1 A_light1();
		goto lightdone;

// MODERN GUNS

	Ready.Modern:
		TNT1 A 0 A_Overlay(5,"none",0);
		TNT1 A 0 A_JumpIfNoAmmo(2);
		PIBN A 1 BD_WeaponReady();
		goto ready;
		PIBE C 1 BD_WeaponReady();
		goto ready;
	Select.Modern:
		TNT1 A 0 A_PlaySound("weapons/pistol/draw");
		TNT1 AAAAAAAA 0 A_Raise;
		PIBS BCDE 1 A_WeaponReady(WRF_NOFIRE);
		goto ready.Modern;
	Deselect.Modern:
		TNT1 A 0 A_JumpIfNoAmmo(3);
		PIBN A 1 A_Lower();
		TNT1 A 0 A_Lower();
		loop;
		PIBE C 1 A_Lower();
		TNT1 A 0 A_Lower();
		loop;
	Fire.Modern:
		TNT1 A 0 A_JumpIfNoAmmo("DryReload");
		PIBF A 1 {
			A_gunflash("Flash.Modern");
			A_FireBullets(1.9,1.3,-1,0,"NullPuff",FBF_USEAMMO,0,"BD_PistolBullet",0,0);
			A_PlaySound("Mweapons/pistol/fire",4);
			}
		TNT1 A 0 A_SetPitch(pitch-frandom(0.9,1.3),SPF_INTERPOLATE);
		PIBN ABC 1;
		TNT1 A 0 { If(Bdoom_debris == 1)
				A_SpawnItemEx("9mmCasing",random(3,4),cos(pitch)*-25,sin(-pitch)*25+random(31,32),		random(1,3),0,random(4,6),	random(-80,-90),0, SXF_ABSOLUTEMOMENTUM); 
				}
		TNT1 A 0 A_JumpIfNoAmmo(6);
		PIBN DEEFF 1 A_WeaponReady(WRF_NOSECONDARY);
		goto ready.Modern;
		PIBN DCB 2;
		PIBE C 2;
		goto ready.Modern+3;
	Flash.Modern:
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_muzzle")==1,3);
		PI0F L 1 bright A_light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		TNT1 A 0 A_Jump(256,1,3,5,7);
		PI0L A 1 bright A_light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PI0L B 1 bright A_light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PI0L C 1 bright A_light1();
		TNT1 A 1 A_light1();
		goto lightdone;
		PI0L D 1 bright A_light1();
		TNT1 A 1 A_light1();
		goto lightdone;
	DryReload:
		PIBE C 18 A_PlaySound("Mweapons/dry");
	Reload:
		TNT1 A 0 {
			if (CountInv("Clip") == 0)
				return ResolveState("Ready");
			If(CountInv("PistolClip") > 0)
				return ResolveState(3);
			return ResolveState(null);
			}
		PIBE C 1 { invoker.wasempty = 1; }
		PIBE C 0 A_Jump(256,2);
		PIBN A 1;
		#### ##### 1 A_WeaponOffset(1.0,2.0,WOF_ADD);
		#### # 0 A_PlaySound("Mweapons/pistol/clipout",5);
		#### # 19 A_WeaponOffset(0.0,0.0,WOF_ADD);
		#### # 0 {
			A_PlaySound("Mweapons/pistol/clipin",5);
			BD_Reload();
			}
		#### # 1 A_WeaponOffset(0.0,-3.0,WOF_ADD);
		#### ### 1 A_WeaponOffset(0.0,1.0,WOF_ADD);
		#### # 6 A_WeaponOffset(0.0,0.0,WOF_ADD);
		#### ##### 1 A_WeaponOffset(-1.0,-2.0,WOF_ADD);
		TNT1 A 0 A_JumpIf (invoker.wasempty == 1, 1);
		goto ready;

		TNT1 A 0 {
			A_PlaySound("Mweapons/pistol/cock");
			invoker.wasempty = 0;
			}
		PIBE CBA 2;
		PIBN A 3;
		TNT1 A 0 BD_Reload();
		goto ready;
	}
}


Class PistolClip : Ammo
{
Default {
inventory.icon "HBULZ0";
inventory.amount 1;
inventory.maxamount 15;
ammo.backpackamount 0;
ammo.backpackmaxamount 15;
	}
}