Class BD_SpiderMastermind : BdoomMonster replaces SpiderMastermind
{
Default
	{
	Health 3000;
	Radius 128;
	Height 100;
	Mass 1000;
	Speed 12;
	PainChance 40;
	Monster;
	+BOSS
	+MISSILEMORE
	+FLOORCLIP
	+NORADIUSDMG
	+DONTMORPH
	+BOSSDEATH
	SeeSound "spider/sight";
	PainSound "spider/pain";
	DeathSound "spider/death";
	ActiveSound "spider/active";
	Obituary "$OB_SPIDER";
	Tag "$FN_SPIDER";
	BdoomMonster.allowfiredeath false;
	BdoomMonster.allowsmokedeath false;
	}
void DyingSpark() {
	if (bdoom_blood) {
		for (int i = 3; i > 0; i--)
			A_SpawnItemEx("Blood",frandom(-10,10),frandom(-10,10),frandom(64,80),frandom(-6,6),frandom(-6,6),frandom(3,7),SXF_SETTARGET,failchance:64);
		}
	if (bdoom_debris) {
		for (int i = 4; i > 0; i--)
			A_SpawnItemEx("RicochetSpark",0,frandom(-16,16),frandom(48,70),frandom(-4,4),frandom(-4,4),frandom(4,8),failchance:64);
		}
	}
states
	{
	Raise:
		stop;
	Death.Fire:
		stop;
	Death.Plasmadamage:
		stop;
	Death.BFGSplash:
		stop;
	Spawn: 
		SMMA A 10 A_Look();
		loop;
	see:
		SMMA a 3 a_metal;
		SMMA a 3 a_chase;
		SMMA b 3 a_chase;
		SMMA c 3 a_chase;
		SMMA d 3 a_metal;
		SMMA e 3 a_chase;
		SMMA e 3 a_chase;
		SMMA f 3 a_chase;
		SMMA g 3 a_metal;
		SMMA h 3 a_chase;
		SMMA i 3 a_chase;
		SMMA j 3 a_chase;
		loop;

	Missile:
		TNT1 A 0 {
			A_FaceTarget();
			A_Playsound("weapons/chaingun/start",5,attenuation:1);
			}
		SSMB ABCDEF 1;
		SSMB ABCDEF 1;
		SSMB ACEACEAD 1;
		TNT1 A 0 {
			if (bdoom_weapons == 0)
				return ResolveState("Missile.Vanilla");
			else if (bdoom_weapons == 1)
				return ResolveState("Missile.Enhanced");
			else if (bdoom_weapons == 2)
				return ResolveState("Missile.Modern");
			return ResolveState(null);
			}
	Missile.Vanilla:
		SSMA B 1 BD_BulletAttack(22.5, 0, 3, random(1,5)*3,"spider/attack","spiderdemon/fire","ChaingunCasing",12,-15,43,43,10);
		SSMA CAD 1;
		SSMA B 1 BD_BulletAttack(22.5, 0, 3, random(1,5)*3,"spider/attack","spiderdemon/fire","ChaingunCasing",12,-15,43,43,10);
		SSMA CAD 1;
		SSMA D 1 A_SpidRefire;
		TNT1 A 0 {
			if (bdoom_weapons == 0)
				return ResolveState("Missile.Vanilla");
			else if (bdoom_weapons == 1)
				return ResolveState("Missile.Enhanced");
			else if (bdoom_weapons == 2)
				return ResolveState("Missile.Modern");
			return ResolveState(null);
			}
		loop;
	Missile.Enhanced: //firing twice as fast but per-shot damage is approximately halved 
		SSMA B 1 BD_BulletAttack(22.5, 0, 3, random(1,5)*1.5,"spider/attack","spiderdemon/fire","ChaingunCasing",12,-15,43,43,10);
		SSMA A 1;
		SSMA B 1 BD_BulletAttack(22.5, 0, 3, random(1,5)*1.5,"spider/attack","spiderdemon/fire","ChaingunCasing",12,-15,43,43,10);
		SSMA A 1;
		SSMA D 1 A_SpidRefire;
		TNT1 A 0 {
			if (bdoom_weapons == 0)
				return ResolveState("Missile.Vanilla");
			else if (bdoom_weapons == 1)
				return ResolveState("Missile.Enhanced");
			else if (bdoom_weapons == 2)
				return ResolveState("Missile.Modern");
			return ResolveState(null);
			}
		loop;
	Missile.Modern:
		SSMA B 1 {
			A_PlaySound("spiderdemon/fire");
			A_SpawnItemEx("ChaingunCasing",12,-15,40, frandom(2,4),0,random(2,4), random(-85,-95),SXF_SETTARGET);
			A_CustomBulletAttack(5.0, 1, 1, 0, "NUllPuff", 0, CBAF_NORANDOM,AAPTR_TARGET,"BD_SpiderBullet",43,10);
			}
		SSMA A 1;
		SSMA B 1 {
			A_PlaySound("spiderdemon/fire");		
			A_SpawnItemEx("ChaingunCasing",12,-15,40, frandom(2,4),0,random(2,4), random(-85,-95),SXF_SETTARGET);
			A_CustomBulletAttack(5.0, 1, 1, 0, "NUllPuff", 0, CBAF_NORANDOM,AAPTR_TARGET,"BD_SpiderBullet",43,10);	
			}
		SSMA A 1;
		SSMA D 1 A_SpidRefire;
		TNT1 A 0 {
			if (bdoom_weapons == 0)
				return ResolveState("Missile.Vanilla");
			else if (bdoom_weapons == 1)
				return ResolveState("Missile.Enhanced");
			else if (bdoom_weapons == 2)
				return ResolveState("Missile.Modern");
			return ResolveState(null);
			}
		loop;
	pain:
		SPMA a 2;
		SPMA b 2;
		SPMA c 2 a_pain;
		goto see;	
	death:
		TNT1 A 0 {
			if (bdoom_gibs && !bdoom_nobossgibs)
				return ResolveState("Death.Particles");
			return ResolveState(null);
			}
		SDMA a 20 a_scream;
		SDMA b 5 a_noblocking;
		SDMA c 5;
		SDMA defghijklmnopq 4;
		SDMA r 30;
		SDMA r -1 a_bossdeath;
		stop;
	Death.Particles:
		TNT1 A 0 A_PlaySound("spiderdemon/shutdown");
		SPMA ABCA 5 DyingSpark();
		TNT1 A 0 A_NoBlocking;
		SMMA A 5;
		SPMA ABCBA 5 DyingSpark();
		SMMA A 10;
		SPIX ABCDEF 5 DyingSpark();	
		TNT1 A 0 {
			A_PlaySound("spiderdemon/explosion",attenuation:ATTN_NONE);
			A_SpawnItemEx("SpiderExplosion",0,0,72);
			
			A_FaceTarget();
			A_SpawnItemEx("SpiderBrainLeft",0,-16,64, frandom(-1.5,1.5),frandom(-3.5,-5.5),frandom(4,6),flags:SXF_SETTARGET);
			A_SpawnItemEx("SpiderBrainRight",0,16,64, frandom(-1.5,1.5),frandom(3.5,5.5),frandom(4,6),flags:SXF_SETTARGET);
			A_SpawnItemEx("SpiderBrainMid",-16,0,64, frandom(-3.5,-5.5),frandom(-1.5,1.5),frandom(4,6),flags:SXF_SETTARGET);
			A_SpawnItemEx("SpiderFace",16,0,64, frandom(3.5,5.5),frandom(-1.5,1.5),frandom(4,6),flags:SXF_SETTARGET);
			
			A_SpawnItemEx("SpiderLegLeft",16,-48,60, frandom(2,3),frandom(-4.5,-5.5),frandom(7,9),flags:SXF_SETTARGET);
			A_SpawnItemEx("SpiderLegLeft",-16,-48,60, frandom(-2,-3),frandom(-4.5,-5.5),frandom(7,9),flags:SXF_SETTARGET);
			A_SpawnItemEx("SpiderLegRight",16,48,60, frandom(2,3),frandom(4.5,5.5),frandom(7,9),flags:SXF_SETTARGET);
			A_SpawnItemEx("SpiderLegRight",-16,48,60, frandom(-2,-3),frandom(4.5,5.5),frandom(7,9),flags:SXF_SETTARGET);
			
			A_SpawnItemEx("SpiderHandLeft",0,-16,64, frandom(-1.5,1.5),frandom(-3,-4.5),frandom(6,7),flags:SXF_SETTARGET);
			A_SpawnItemEx("SpiderHandRight",0,16,64, frandom(-1.5,1.5),frandom(3,4.5),frandom(5,7),flags:SXF_SETTARGET);
			
			for (int i = 10; i > 0; i--)
				A_SpawnItemEx("BFountainSpray",frandom(-5.0,5.0),frandom(-5.0,5.0),random(24,72),frandom(-1.5,1.5),frandom(-1.5,1.5),frandom(4.5,6.5),0,SXF_SETTARGET,36);
			for (int i = 6; i > 0; i--)
				A_SpawnItemEx("BasicMeatPiece_Heavy",random(-4,4),random(-4,4),random(24,72),	frandom(-5,5),frandom(-5,5),random(5,8),	0,	SXF_SETTARGET, 64);
			for (int i = 15; i > 0; i--)
				A_SpawnItemEx("BasicMetalPiece",random(-4,4),random(-4,4),random(24,72),		frandom(-10,10),frandom(-10,10),random(5,10),	0,SXF_SETTARGET, 64);
			for (int i = 8; i > 0; i--)
				A_SpawnItemEx("SpiderMetalPiece",random(-4,4),random(-4,4),random(24,72),		frandom(-10,10),frandom(-10,10),random(5,10),	0,SXF_SETTARGET, 64);
			A_SpawnItemEx("SpiderGunGib",16,0,50, frandom(3.5,5.5),frandom(-1.5,1.5),frandom(4,6),flags:SXF_SETTARGET);
			}
		TNT1 A 30;
		TNT1 A 1 A_BossDeath;
		stop;
		}
}

Class SpiderExplosion : Actor
{
Default {
	+NOINTERACTION
	+NOGRAVITY
	renderstyle "Add";
	alpha 0.9;
	scale 0.7;
	}
states
	{
	Spawn:
		TNT1 A 0 NoDelay {
			if (bdoom_shakes)
				Radius_Quake(4,40,0,18,0);
			}
		BOM1 ABCDEFGHIJKLMNOPQRSTUVWX 2;
		stop;
		}		
}
