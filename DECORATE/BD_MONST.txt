//*****************************\\
//*****************************\\
//****** M O N S T E R S ******\\
//*****************************\\
//*****************************\\

Actor BD_KillChecker : DummyChecker
{
inventory.maxamount 10
}

Actor BD_MonsterClip : DummyChecker
{
inventory.maxamount 50
}

Actor ParticleFire_Red : ParticleFire_General
{
states
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf(GetCVAR("bdoom_Flames")==0,"Null")
		TNT1 A 0 A_CheckSight("Null")
		TNT1 A 0 A_SetScale(scalex*randompick(-1,1),scaley)
		FIPR ABCDEFGHIJKLMNOPQRST 1 bright A_FadeOut(0.03)
		stop
	}
}

Actor ParticleFire_Yellow : ParticleFire_General
{
renderstyle Add
alpha 0.6
states
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf(GetCVAR("bdoom_Flames")==0,"Null")
		TNT1 A 0 A_CheckSight("Null")
		TNT1 A 0 A_SetScale(scalex*randompick(-1,1),scaley)
		FIPY ABCDEFGHIJKLMNOPQRST 2 bright A_FadeOut(0.07)
		stop
	}
}

Actor ParticleFire_Green : ParticleFire_Red
{ //translate to green
translation "0:3=112:127","5:8=112:127","9:12=112:127","13:15=112:127","16:47=112:127","48:79=112:127","96:111=112:127","112:127=112:127","128:151=112:127","152:159=112:127","160:167=112:127","169:191=112:127","193:207=112:127","208:223=112:127","224:231=112:127","232:235=112:127","236:239=112:127","240:247=112:127","248:249=112:127","250:254=112:127","255:255=112:127"
}

Actor CorpseFlySpawner
{
+NOINTERACTION
+NOGRAVITY
states
		{
		Spawn: //always spawns and waits
			TNT1 A 105
			TNT1 A 0 A_Warp(AAPTR_MASTER,0,0,0,0)
			TNT1 A 0 A_Jump(256,1,2,3,4,5,6,7,8,9,10)
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 300 A_JumpIfInventory("WasGibbed",1,"Null") //approx. 2-4 minutes
		SpawnFlies:	
			TNT1 A 30 A_JumpIfInventory("WasGibbed",1,"Null") //checks if the corpse is still present
			TNT1 A 0 A_TakeInventory("DummyChecker",500)
			TNT1 A 0 A_RemoveChildren(1,RMVF_EVERYTHING)	
			TNT1 A 0 A_Jumpif(GetCvar("bdoom_flies")==1,"Spawn1") //and continues to actual spawning if CVAR allows it
			loop
		Spawn1: //actual fly spawning
			TNT1 A 0 A_GiveInventory("DummyChecker",1)
			TNT1 A 0 A_Jumpif(GetCvar("bdoom_flies")==0,"SpawnFlies")
			TNT1 A 70 A_JumpIfInventory("DummyChecker",15,1)
			loop
			TNT1 A 0 A_SpawnItemEx("CorpseFly",random(-8,8),random(-8,8),random(28,34),0,0,0,0,SXF_SETMASTER)
		Spawn2: //actual fly spawning
			TNT1 A 0 A_GiveInventory("DummyChecker",1)
			TNT1 A 0 A_Jumpif(GetCvar("bdoom_flies")==0,"SpawnFlies")
			TNT1 A 70 A_JumpIfInventory("DummyChecker",45,1)
			loop
			TNT1 A 0 A_SpawnItemEx("CorpseFly",random(-8,8),random(-8,8),random(28,34),0,0,0,0,SXF_SETMASTER)
		Spawn3: //actual fly spawning
			TNT1 A 0 A_GiveInventory("DummyChecker",1)
			TNT1 A 0 A_Jumpif(GetCvar("bdoom_flies")==0,"SpawnFlies")
			TNT1 A 70 A_JumpIfInventory("DummyChecker",75,1)
			loop
			TNT1 A 0 A_SpawnItemEx("CorpseFly",random(-8,8),random(-8,8),random(28,34),0,0,0,0,SXF_SETMASTER,128)
		Spawn4: //actual fly spawning
			TNT1 A 0 A_GiveInventory("DummyChecker",1)
			TNT1 A 0 A_Jumpif(GetCvar("bdoom_flies")==0,"SpawnFlies")
			TNT1 A 70 A_JumpIfInventory("DummyChecker",120,1)
			loop
			TNT1 A 0 A_SpawnItemEx("CorpseFly",random(-8,8),random(-8,8),random(28,34),0,0,0,0,SXF_SETMASTER,128)
		Spawn5: //actual fly spawning
			TNT1 A 0 A_GiveInventory("DummyChecker",1)
			TNT1 A 0 A_Jumpif(GetCvar("bdoom_flies")==0,"SpawnFlies")
			TNT1 A 70 A_JumpIfInventory("DummyChecker",160,1)
			loop
			TNT1 A 0 A_SpawnItemEx("CorpseFly",random(-8,8),random(-8,8),random(28,34),0,0,0,0,SXF_SETMASTER,128)
		SpawnIdle:
			TNT1 A 70 A_Jumpif(GetCvar("bdoom_flies")==0,"SpawnFlies") //checks for CVAR
			TNT1 A 0 A_JumpIfInventory("WasGibbed",1,"Null") //and if the corpse is still present
			loop
		}
}	


Actor CorpseFly
{
+NOGRAVITY
+FLOATBOB
+FORCEXYBILLBOARD
+NOINTERACTION
floatbobstrength 0.5
renderstyle translucent
alpha 0.0
scale .05
states
	{
		Spawn:
			TNT1 A 0 NoDelay A_PlaySound("misc/flyloop",4,1.0,1,14)
			FLYC ABCB 1 A_FadeIn(0.05,FTF_CLAMP)
			FLYC ABCB 1 A_FadeIn(0.05,FTF_CLAMP)
			FLYC ABCB 1 A_FadeIn(0.05,FTF_CLAMP)
			FLYC ABCB 1 A_FadeIn(0.05,FTF_CLAMP)
			FLYC ABCB 1 A_FadeIn(0.05,FTF_CLAMP)
		Fly:
			TNT1 A 0 A_SetScale(scalex*randompick(-1,1),scaley)
			TNT1 A 0 A_FaceMaster
			TNT1 A 0 ThrustThing(angle*256/360+16,1,0,0)
			FLYC ABCBABCB 1
			TNT1 A 0 A_Jump(256,1,5,9)
			FLYC ABCBABCB 1
			FLYC ABCBABCB 1
			TNT1 A 0 A_Stop
			TNT1 A 0 A_JumpIfMasterCloser(64,"Fly",1) //checks if spawner is still present (i.e. corpse is not resurrected)
			FLYC ABCBABCBABCBABCB 1 A_FadeOut(0.05) //otherwise fades out
			stop
	}
}		



