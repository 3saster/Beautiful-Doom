Actor BD_ZombieMan : ZombieMan replaces Zombieman
{
+DOHARMSPECIES
dropitem "BD_Pistol",40,1
dropitem "Clip",256
attacksound ""
states
	{
	Spawn:
		PSSS AB 10 A_Look
		loop
	See:
		TNT1 A 0 A_SetScale(1.0,1.0)
	    POS1 ABCDDCBA 4 A_Chase
		Loop 	
	Crush:
		CRS1 A -1
		stop
	Missile:
		TNT1 A 0 A_Jumpif(GetCVAR("bdoom_weapons")==2,"MissileModern")
		PSSA A 5 A_FaceTarget
		PSSA B 2 A_FaceTarget
		POS1 E 3 A_FaceTarget
			TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Debris")==0,2)
			TNT1 A 0  A_SpawnItemEx("Monster9mmCasing",random(4,6),-22,random(37,38),		random(1,2),0,random(4,6),	random(-85,-95))
				TNT1 A 0 A_JumpIf(GetCVAR("bdoom_weapons")==1,3)
				TNT1 A 0 A_PlaySound("grunt/attack")
				TNT1 A 0 A_Jump(256,2)
				TNT1 A 0 A_PlaySound("weapons/pistol/fire")
		TNT1 A 0 A_JumpIf(GetCVAR("Bdoom_tracers")==0,3)
		POS1 F 3 A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "EnBulletPuff", 0, CBAF_NORANDOM,AAPTR_TARGET,"BulletTracer",29,45)
		TNT1 A 0 A_Jump(256,2)
		POS1 F 3 A_CustomBulletAttack(22.5, 0, 1, random(1,5)*3, "EnBulletPuff", 0, CBAF_NORANDOM) //same as A_PosAttack but with EnBulletPuff
		POS1 E 3 
		PSSA BA 4
		PSSA A 2
		goto See
	MissileModern:
		PSSA A 5 A_FaceTarget
		PSSA B 2 A_FaceTarget
		POS1 E 3 A_FaceTarget
			TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Debris")==0,2)
			TNT1 A 0  A_SpawnItemEx("Monster9mmCasing",random(4,6),-22,random(37,38),		random(1,2),0,random(4,6),	random(-85,-95))
		TNT1 A 0 A_PlaySound("Mweapons/pistol/fire")
		POS1 F 3 A_CustomBulletAttack(17.0,1.3,1,0,"NullPuff",0,0,AAPTR_TARGET,"BD_ZombieBullet",29,45)
		POS1 E 14 A_GiveINventory("BD_MonsterClip",1)
		TNT1 A 0 A_JumpIfInventory("BD_MonsterClip",15,"Reload")
		TNT1 A 0 A_MonsterRefire(80,"See")
		goto MissileModern+3
	Reload:
		TNT1 A 0 A_ChangeFlag("FRIGHTENED",1)
		TNT1 A 0 A_PlaySound("Mweapons/pistol/clipout")
		TNT1 A 0 A_TakeInventory("BD_MonsterClip",50)
	    POS1 ABCDDCBA 4 A_Chase(0,0)
	    POS1 ABCDDCBA 4 A_Chase(0,0)
		TNT1 A 0 A_PlaySound("Mweapons/pistol/clipin")
	    POS1 ABCDDCBA 4 A_Chase(0,0)
		TNT1 A 0 A_ChangeFlag("FRIGHTENED",0)
		goto see
	Pain:
		stro a 3
		stro b 3 a_pain
		goto see
	Death:
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Gibs")==0,2)
		TNT1 A 0 A_JumpIfHealthLower(-10,"DecapDeath")
		TNT1 A 0 A_Jump(256,"Death.Vanilla","Death1","Death2")
	Death.Vanilla:
		TNT1 A 0 A_SpawnItemEx("Blood",random(-5,5),0,random(16,20),0,0,0,SXF_SETTARGET)
		POSM A 3 A_Scream
		POSM B 3 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx("Blood",random(-5,5),0,random(16,20),0,0,0,SXF_SETTARGET)
		POSM CDEFG 3
		POSM H 3
		TNT1 A 0 A_SpawnItemEx("CorpseFlySpawner",0,0,0,0,0,0,0,SXF_SETMASTER)
		POSM I -1
		Stop
	Death1: //fall forward
			TNT1 A 0 A_GiveInventory("BD_KillChecker",1)
		POSO A 3 A_SpawnItemEx("Blood",random(-5,5),0,random(16,20),0,0,0,SXF_SETTARGET)
		POSO B 3 A_Scream
		TNT1 A 0 A_SpawnItemEx("Blood",random(-5,5),0,random(16,20),0,0,0,SXF_SETTARGET)
		POSO C 3 A_NoBlocking
		POSO D 3 A_SpawnItemEx("Blood",random(-5,5),0,random(16,20),0,0,0,SXF_SETTARGET)
		POSO EFGHI 3
		POSO JK 4
		TNT1 A 0 A_SpawnItemEx("CorpseFlySpawner",0,0,0,0,0,0,0,SXF_SETMASTER)
		POSO L -1
		stop
	Death2: //fall left/right
		TNT1 A 0 A_GiveInventory("BD_KillChecker",2)
		TNT1 A 0 A_SetScale(randompick(-1.0,1.0),1.0)
		TNT1 A 0 A_Jump(128,"Death3")
		TNT1 A 0 A_Scream
		POSN ABC 2 A_SpawnItemEx("BloodDrop",random(-2,2),0,random(32,34),	random(4,6),0,random(3,5),random(-70,-90)*scalex,SXF_SETMASTER)
		TNT1 A 0 A_NoBlocking
		POSN DEF 3 A_SpawnItemEx("BloodDrop",random(-2,2),0,random(24,26),	random(4,6),0,random(3,5),random(-70,-90)*scalex,SXF_SETMASTER)
		POSN GHI 3 A_SpawnItemEx("BloodDrop",random(-2,2),0,random(10,16),	random(4,6),0,random(3,5),random(-70,-90)*scalex,SXF_SETMASTER)
		POSN KLM 3
		TNT1 A 0 A_SpawnItemEx("CorpseFlySpawner",0,0,0,0,0,0,0,SXF_SETMASTER)
		POSN N -1
		stop
	Death.Saw:
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Gibs")==0,"Death")
		TNT1 A 0 A_Jump(128,"Death")
		TNT1 A 0 A_GiveInventory("BD_KillChecker",3)
		TNT1 A 0 A_SetScale(randompick(-1.0,1.0),1.0)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Pain
			TNT1 A 0 A_Jump(160,2,3,4)
		POSY AAAZZZ 6 	A_SpawnItemEx("BloodSprayer",	random(-4,4),random(-4,-4),random(24,26),0,0,0,0,SXF_SETTARGET)
		POSY Z 5 A_Pain
		TNT1 A 0 A_NoBlocking
			TNT1 A 0 A_Jump(80,2,3)
		POSY AAAZZ 7 	A_SpawnItemEx("BloodSprayer",	random(-4,4),random(-4,-4),random(24,26),0,0,0,0,SXF_SETTARGET)
		POSY Z 5 A_Scream
		POSY BCDEFG 3 A_SpawnItemEx("Blood",	random(-4,4),random(0,6),	random(12,18),	random(2,4),0,random(4,6),	random(-20,20),SXF_SETTARGET,128)
		TNT1 A 0 A_PlaySound("gibs/meatland")
		POSY HI 2 		A_SpawnItemEx("Blood",	random(-4,4),random(0,6),	random(12,18),	random(2,4),0,random(4,6),	random(-20,20),SXF_SETTARGET,128)
		TNT1 A 0 A_PlaySound("gibs/meatland")
		POSY J 3 		A_SpawnItemEx("Blood",	random(-4,4),random(0,6),	random(12,18),	random(2,4),0,random(4,6),	random(-20,20),SXF_SETTARGET,128)
		TNT1 A 0 A_SpawnItemEx("CorpseFlySpawner",0,0,0,0,0,0,0,SXF_SETMASTER)
		POSY K -1
		stop
	DecapDeath:
		TNT1 A 0 A_GiveInventory("BD_KillChecker",4)
		TNT1 A 0 A_SpawnItemEx("PossHead",random(-2,2),random(-2,2),random(50,54),random(2,5),0,random(1,5),random(150,210),SXF_SETMASTER)
		TNT1 A 0 A_SpawnItemEx("PossHand",0,random(4,8),random(40,45),random(2,5),0,random(1,5),random(-80,-100),SXF_SETMASTER)
		POSD A 2 A_spawnitemEx("BloodSprayer",0,0,random(16,20),0,0,0,0,SXF_SETTARGET)
		POSD B 2
		TNT1 A 0 A_spawnitemEx("BloodSprayer",0,0,random(16,20),0,0,0,0,SXF_SETTARGET)
		POSD C 2 A_NoBlocking
		POSD DE 2 A_spawnitemEx("BloodSprayer",0,0,random(16,20),0,0,0,0,SXF_SETTARGET)
		POSD FG 2
		TNT1 A 0 A_SpawnItemEx("CorpseFlySpawner",0,0,0,0,0,0,0,SXF_SETMASTER)
		POSD H -1
		stop
	Death.BFGSplash:
		TNT1 A 0 Thing_SetTranslation(0,11) //translation to grey+blood
	XDeath:
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Gibs")==1,"XDeath.Particles")
		TNT1 A 0 A_GiveInventory("BD_KillChecker",5)
		ZGIB ab 2
		ZGIB cd 2 A_XScream
		ZGIB e 2 a_noblocking
		ZGIB f 2
		ZGIB ghijklmnop 2
		TNT1 A 0 A_SpawnItemEx("CorpseFlySpawner",0,0,0,0,0,0,0,SXF_SETMASTER)
		ZGIB q -1
		Stop
	XDeath.Particles:
		TNT1 A 0 A_PlaySound("gibs/gibbed")
		TNT1 A 0 A_GiveInventory("WasGibbed",1)

		TNT1 A 0 {
			A_SpawnItemEx("BloodFountain",0,0,0,0,0,0,0,SXF_SETTARGET);
			A_SpawnItemEx("Heart",			random(-4,4),random(-4,4),random(24,32),	velx*frandom(0.35,1.2),vely*frandom(0.35,1.2),random(1,3),	0,					SXF_ABSOLUTEMOMENTUM | SXF_TRANSFERTRANSLATION | SXF_SETMASTER, 220);
			A_SpawnItemEx("PossTorso1",	random(-4,4),random(-4,4),random(5,48),	velx*frandom(0.35,1.2),vely*frandom(0.35,1.2),random(1,2),	random(-45,-135),	SXF_ABSOLUTEMOMENTUM | SXF_TRANSFERTRANSLATION | SXF_SETMASTER);
			A_SpawnItemEx("PossTorso2",	random(-4,4),random(-4,4),random(5,48),	velx*frandom(0.35,1.2),vely*frandom(0.35,1.2),random(1,2),	random(45,135),		SXF_ABSOLUTEMOMENTUM | SXF_TRANSFERTRANSLATION | SXF_SETMASTER);
			A_SpawnItemEx("PossHand",		random(-6,-4),random(-4,4),random(35,40),	velx*frandom(0.35,1.2),vely*frandom(0.35,1.2),random(5,10),	0,	SXF_ABSOLUTEMOMENTUM | SXF_TRANSFERTRANSLATION | SXF_SETMASTER, 48);
			A_SpawnItemEx("PossHand",		random(6,4),random(-4,4),random(35,40),		velx*frandom(0.35,1.2),vely*frandom(0.35,1.2),random(5,10),	0,	SXF_ABSOLUTEMOMENTUM | SXF_TRANSFERTRANSLATION | SXF_SETMASTER, 48);
			A_SpawnItemEx("PossLeg1",		random(-6,-4),random(-4,4),random(10,12),	velx*frandom(0.35,1.2),vely*frandom(0.35,1.2),random(5,10),	0,	SXF_ABSOLUTEMOMENTUM | SXF_TRANSFERTRANSLATION | SXF_SETMASTER, 48);
			A_SpawnItemEx("PossLeg2",		random(6,4),random(-4,4),random(10,12),		velx*frandom(0.35,1.2),vely*frandom(0.35,1.2),random(5,10),	0,	SXF_ABSOLUTEMOMENTUM | SXF_TRANSFERTRANSLATION | SXF_SETMASTER, 48);
			A_SpawnItemEx("PossHead",		random(-2,2),random(-2,2),random(42,48),	velx*frandom(0.35,1.2),vely*frandom(0.35,1.2),random(5,10),	0,	SXF_ABSOLUTEMOMENTUM | SXF_TRANSFERTRANSLATION | SXF_SETMASTER, 48);
			/*for (int i = 7; i > 0; i--)
				A_SpawnItemEx("MeatPiece",random(-4,4),random(-4,4),random(10,48),		velx*frandom(0.35,1.2),vely*frandom(0.35,1.2),random(5,10),	0,	SXF_ABSOLUTEMOMENTUM | SXF_TRANSFERTRANSLATION | SXF_SETMASTER,	80);*/
			}
		TNT1 AAAAAAA 0 A_SpawnItemEx("MeatPiece",random(-4,4),random(-4,4),random(10,48),		velx*frandom(0.35,1.2),vely*frandom(0.35,1.2),random(5,10),	0,	SXF_ABSOLUTEMOMENTUM | SXF_TRANSFERTRANSLATION | SXF_SETMASTER,	80)
		POSX AB 2
		POSX C 2 A_XScream
		POSX D 2 A_NoBlocking
		POSX EFGHIJKLMN 2
		TNT1 A 0 A_SpawnItemEx("CorpseFlySpawner",0,0,0,0,0,0,0,SXF_SETMASTER)
		POSX O -1
		stop
	Death.Massacre:
	Death.Telefrag:
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Gibs")==0,"XDeath")
		TNT1 A 0 A_PlaySound("gibs/gibbed")
		TNT1 A 0 A_GiveInventory("WasGibbed",1)
		TNT1 AAAAAAAAAAA 0 A_SpawnItemEx("Blood",	random(-4,4),random(-4,4),random(8,48),	random(1,3),random(1,3),random(5,10),	random(0,360), SXF_SETMASTER, 48)
		POSX AB 2
		POSX C 2 A_XScream
		POSX D 2 A_NoBlocking
		POSX EFGHIJKLMN 2
		POSX O -1
		stop
	Death.PlasmaDamage:
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Gibs")==0,"Death")
		TNT1 A 0 Thing_SetTranslation(0,7) //translation to grey
		TNT1 A 0 A_SetScale(randompick(-1.0,1.0),1.0)

		TNT1 A 0  A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		POSM A 3 A_Scream
		TNT1 A 0  A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		POSM B 3 A_NoBlocking
		TNT1 A 0  A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		POSM CDE 2
		TNT1 A 0  A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		POSM FG 2
		POSM H 3
		POSM IIIII 6 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		POSM IIIII 9 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		POSM IIIII 12 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		POSM I -1
		Stop
	Death.Fire:
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Gibs")==0,"Death")
		TNT1 A 0 A_SetScale(randompick(-1.0,1.0),1.0)

		TNT1 A 0 Thing_SetTranslation(0,8) //translation to scorched orange-red
		TNT1 A 0 A_PlaySoundEx("world/torch","Body",1,-1)

		POSM A 1 A_Scream
		POSM AA 1 A_SpawnItemEx("ParticleFire_Yellow",random(-16,16),random(-16,16),random(8,16),0,0,2)
		POSM B 1 A_NoBlocking
		POSM BB 1 A_SpawnItemEx("ParticleFire_Yellow",random(-16,16),random(-16,16),random(8,16),0,0,2)
		POSM CCDDEEFFGG 1 A_SpawnItemEx("ParticleFire_Yellow",random(-16,16),random(-16,16),random(8,16),0,0,2)
		POSM HHH 1 A_SpawnItemEx("ParticleFire_Yellow",random(-16,16),random(-16,16),random(8,16),0,0,2)
		POSM IIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIII 1 A_SpawnItemEx("ParticleFire_Yellow",random(-16,16),random(-16,16),random(8,16),0,0,2)
		TNT1 A 0 A_StopSoundEx("Body")
		TNT1 A 0 Thing_SetTranslation(0,7) //translation to grey
		POSM IIIII 6 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		POSM IIIII 9 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		POSM IIIII 12 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		POSM I -1
		Stop
	Death.GreenFire:
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Gibs")==0,"Death")
		TNT1 A 0 A_SetScale(randompick(-1.0,1.0),1.0)
		TNT1 A 0 Thing_SetTranslation(0,14) //translation to scorched green
		TNT1 A 0 A_PlaySoundEx("world/torch","Body",1,-1)

		POSM A 1 A_Scream
		POSM AA 1 A_SpawnItemEx("ParticleFire_Green",random(-16,16),random(-16,16),random(8,16),0,0,2)
		POSM B 1 A_NoBlocking
		POSM BB 1 A_SpawnItemEx("ParticleFire_Green",random(-16,16),random(-16,16),random(8,16),0,0,2)
		POSM CCDDEEFFGG 1 A_SpawnItemEx("ParticleFire_Green",random(-16,16),random(-16,16),random(8,16),0,0,2)
		POSM HHH 1 A_SpawnItemEx("ParticleFire_Green",random(-16,16),random(-16,16),random(8,16),0,0,2)
		POSM IIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIII 1 A_SpawnItemEx("ParticleFire_Green",random(-16,16),random(-16,16),random(8,16),0,0,2)
		TNT1 A 0 A_StopSoundEx("Body")
		TNT1 A 0 Thing_SetTranslation(0,7) //translation to grey
		POSM IIIII 6 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		POSM IIIII 9 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		POSM IIIII 12 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(8,16),0,0,1)
		POSM I -1
		Stop
	Raise:
		TNT1 A 0 A_GiveToChildren("WasGibbed",1)
		TNT1 A 0 Thing_SetTranslation(0,10)
		TNT1 A 0 A_JumpIfInventory("WasGibbed",1,"XRaise.Particles") //if particle gibbing was applied
		TNT1 A 0 A_JumpIfInventory("BD_KillChecker",5,"XRaise") //vanilla gibbing
		TNT1 A 0 A_JumpIfInventory("BD_KillChecker",4,"DecapRaise")
		TNT1 A 0 A_JumpIfInventory("BD_KillChecker",3,"SawRaise")
		TNT1 A 0 A_JumpIfInventory("BD_KillChecker",2,"Raise3")
		TNT1 A 0 A_JumpIfInventory("BD_KillChecker",1,"Raise2")
		TNT1 A 0 A_TakeInventory("BD_KillChecker",10)
		POSM HGFEDCBA 2
		TNT1 A 0 A_SetScale(1.0,1.0)
		Goto See
	Raise2: //fell forward
		TNT1 A 0 A_TakeInventory("BD_KillChecker",10)
		POSO KJIHGFEDCBA 2
		TNT1 A 0 A_SetScale(1.0,1.0)
		goto See
	Raise3: //fell to the side
		TNT1 A 0 A_TakeInventory("BD_KillChecker",10)
		POSN MLKJIHGFEDCBA 2
		TNT1 A 0 A_SetScale(1.0,1.0)
		goto See
	SawRaise:
		TNT1 A 0 A_TakeInventory("BD_KillChecker",10)
		POSY JIHGFEDCBA 2
		TNT1 A 0 A_SetScale(1.0,1.0)
		goto See
	DecapRaise:
		TNT1 A 0 A_TakeInventory("BD_KillChecker",10)
		POSD FEDCBA 3
		TNT1 A 0 A_SetScale(1.0,1.0)
		goto See
	XRaise:
		TNT1 A 0 A_TakeInventory("BD_KillChecker",10)
		ZGIB PONMLKJIHQFEDCBA 1
		goto See
	XRaise.Particles:
		TNT1 A 0 A_TakeInventory("WasGibbed",1)
		POSX NMLKJ 1 
		POSX IHQFEDCBA 2
		TNT1 A 0 A_SetScale(1.0,1.0)
		goto See
		}
}

Actor BD_ZombieBullet : RealBullet
{
damage (3 * random(1,5))
}