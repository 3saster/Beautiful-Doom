Actor BD_Cacodemon : Cacodemon replaces Cacodemon
{
+FORCEXYBILLBOARD
bloodcolor "00 00 CC"
gibhealth 50
+FLOATBOB
FloatBobStrength 0.35
states
	{
	Spawn:
		HEA1 AAAAA 10 A_Look
		HEA1 B 5 A_Look
		HEA1 C 5
		HEA1 B 5 A_Look
		HEA1 A 5
		loop
	See:
		TNT1 A 0 A_SetScale(1.0,1.0)
		TNT1 A 0 A_ChangeFlag("FLOATBOB",0)
		HEA1 AAAAAAAAAA 3 A_Chase
		HEA1 B 3 A_Chase
		HEA1 C 3 A_Chase
		HEA1 B 3 A_Chase
		HEA1 A 3 A_Chase
		loop
	Missile:
		HEAA ABCCDD 1 A_FaceTarget
		HEAA E 4  A_HeadAttack
		HEAA CCA 1 
		Goto See
	Pain:
		TNT1 A 0 A_ChangeFlag("FLOATBOB",0)
		HEAA F 3
		HEAA G 3 A_Pain
		HEAA HGF 2
		Goto See
	Crush:
		CRS2 B -1
		stop
	Death:
		TNT1 A 0 A_ChangeFlag("FLOATBOB",0)
		TNT1 A 0 A_SetScale(randompick(-1.0,1.0),1.0)
		TNT1 A 0 A_ChangeFlag("FORCEXYBILLBOARD",0)
		TNT1 A 0 A_jump(212,"Death.Vanilla","Death1")
		TNT1 A 0 A_Jump(128,"Death2")
	Death.Vanilla:	
		HEAM A 3 A_spawnitemEx("BloodDrop",0,0,random(16,20),frandom(2.0,4.0),frandom(2.0,4.0),frandom(2.0,4.0),random(0,360),SXF_SETTARGET)
		HEAM B 3 A_Scream
		HEAM CD 3 A_spawnitemEx("BloodDrop",0,0,random(16,20),frandom(2.0,4.0),frandom(2.0,4.0),frandom(2.0,4.0),random(0,360),SXF_SETTARGET)
		HEAM E 3 A_NoBlocking
		HEAM F 3 A_SetFloorClip
		HEAM GH 3
		HEAM I -1
		Stop
	Death1: //no eye, intestines fall down
		TNT1 A 0 A_GiveInventory("BD_KillChecker",1)
		HEAN AB 3
		TNT1 A 0 A_Scream
		HEAN C 3  A_SpawnItemEx("CacoEye",random(-2,2),random(-2,2),random(28,32),random(3,7),0,random(0,5),random(-10,10),SXF_SETMASTER)
		 TNT1 A 0 A_spawnitemEx("BloodDrop",0,0,random(16,20),frandom(2.0,4.0),frandom(2.0,4.0),frandom(2.0,4.0),random(0,360),SXF_SETTARGET)
		HEAN DE 3
		HEAN F 3 A_NoBlocking
		 TNT1 A 0 A_spawnitemEx("BloodDrop",0,0,random(16,20),frandom(2.0,4.0),frandom(2.0,4.0),frandom(2.0,4.0),random(0,360),SXF_SETTARGET)
		HEAN GHIJKL 3
		TNT1 A 0 A_SetFloorClip
		HEAN MN 5
		TNT1 A 0 A_SpawnItemEx("CorpseFlySpawner",0,0,0,0,0,0,0,SXF_SETMASTER)
		HEAN O -1
		stop
	Death2: //puke gibs, uncommon death
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Gibs")==0,"Death")
		TNT1 A 0 A_GiveInventory("BD_KillChecker",2)
		TNT1 A 0 A_ChangeFlag("NOGRAVITY",1)
		HEAP ABC 4
		TNT1 A 0 A_SpawnItemEx("CacoEye",random(-2,2),random(-2,2),random(28,32),random(3,7),0,random(0,5),random(-10,10),SXF_SETMASTER)
		HEAP DDEEFFGGHH 2 A_SpawnItemEx("CacoMeatPiece",0,random(-6,6),random(8,20),	random(3,5),0,random(2,3),	random(-15,15),	SXF_SETMASTER,48)
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_ChangeFlag("NOGRAVITY",0)
		HEAP I 4 A_SetFloorClip
		HEAP JK 4
		TNT1 A 0 A_SpawnItemEx("CorpseFlySpawner",0,0,0,0,0,0,0,SXF_SETMASTER)
		HEAP L -1
		stop
	Death.Saw: //sliced diagonally
		TNT1 A 0 A_ChangeFlag("FLOATBOB",0)
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Gibs")==0,"Death")
		TNT1 A 0 A_Jump(128,"Death")
		TNT1 A 0 A_SetScale(randompick(-1.0,1.0),1.0)
		TNT1 A 0 A_GiveInventory("BD_KillChecker",3)
		HEAM A 3 A_spawnitemEx("BloodDrop",0,0,random(16,20),frandom(2.0,4.0),frandom(2.0,4.0),frandom(2.0,4.0),random(0,360),SXF_SETTARGET)
		HEAM B 3 A_Scream
		TNT1 A 0 A_FaceTarget
		HEAO AABBCCDD 2 A_SpawnItemEx("BloodDrop",0,random(-16,-20),random(10,14),0,random(-5,-8),random(5,7),random(-10,10),SXF_SETMASTER,64)
		TNT1 A 0 A_NoBloCking
		TNT1 A 0 A_SetFloorCLip
		HEAO EF 4
		TNT1 A 0 A_SpawnItemEx("CorpseFlySpawner",0,0,0,0,0,0,0,SXF_SETMASTER)
		HEAO G -1
		stop
	XDeath: //sprite gibbing from Smooth Doom
		TNT1 A 0 A_ChangeFlag("FLOATBOB",0)
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Gibs")==0,"Death")
		TNT1 A 0 A_Jump(128,"Death")
		TNT1 A 0 A_GiveInventory("BD_KillChecker",4)
		TNT1 A 0 A_SetScale(randompick(-1.0,1.0),1.0)
		HEAX ABC 3 A_SpawnItemEx("CacoMeatPiece",random(-6,6),random(-6,6),random(8,20),	random(-6,6),random(-6,6),random(4,6), 0,	SXF_SETMASTER,48)
		TNT1 A 0 A_XScream
		HEAX DEF 3 A_SpawnItemEx("CacoMeatPiece",random(-6,6),random(-6,6),random(8,20),	random(-6,6),random(-6,6),random(4,6), 0,	SXF_SETMASTER,48)
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SetFloorCLip
		HEAX GH 2  A_SpawnItemEx("CacoMeatPiece",random(-6,6),random(-6,6),random(8,20),	random(-6,6),random(-6,6),random(4,6), 0,	SXF_SETMASTER,48)
		HEAX IJKLMNOPQR 2
		TNT1 A 0 A_SpawnItemEx("CorpseFlySpawner",0,0,0,0,0,0,0,SXF_SETMASTER)
		HEAX S -1
		stop
	Death.BFGSplash:
		TNT1 A 0 A_ChangeFlag("FLOATBOB",0)
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Gibs")==0,"Death")
		TNT1 A 0 Thing_SetTranslation(0,12) //translation to grey+blueblood
		goto Death.PlasmaDamage+2
	Death.GreenFire:
	Death.Fire:
	Death.PlasmaDamage:
		TNT1 A 0 A_ChangeFlag("FLOATBOB",0)
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Gibs")==0,"Death")
		TNT1 A 0 Thing_SetTranslation(0,7)
		
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetScale(-1.0,1.0)
		
		HEAM A 3 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(24,38),0,0,1)
		TNT1 A 0 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(24,38),0,0,1)
		HEAM B 3 A_Scream
		HEAM CD 3 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(24,38),0,0,1)
		TNT1 A 0 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(24,38),0,0,1)
		HEAM E 3 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(24,38),0,0,1)
		HEAM F 3 A_SetFloorClip
		HEAM GH 3 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(24,38),0,0,1)
		HEAM IIIII 6 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(24,38),0,0,1)
		HEAM IIIII 9 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(24,38),0,0,1)
		HEAM IIIII 12 A_SpawnItemEx("BlackSmoke",random(-16,16),random(-16,16),random(24,38),0,0,1)
		HEAM I -1
		Stop

	Raise:
		TNT1 A 0 A_GiveToChildren("WasGibbed",1)
		TNT1 A 0 Thing_SetTranslation(0,10)
		TNT1 A 0 A_UnSetFloorClip
		TNT1 A 0 A_ChangeFlag("FORCEXYBILLBOARD",1)
		TNT1 A 0 A_JumpIfInventory("BD_KillChecker",4,"XRaise")
		TNT1 A 0 A_JumpIfInventory("BD_KillChecker",3,"SawRaise")
		TNT1 A 0 A_JumpIfInventory("BD_KillChecker",2,"Raise2")
		TNT1 A 0 A_JumpIfInventory("BD_KillChecker",1,"Raise1")
		
		HEAM IHGFEDCBA 3
		TNT1 A 0 A_SetScale(1.0,1.0)
		goto See
	Raise1:
		TNT1 A 0 A_TakeInventory("BD_KillChecker",10)
		HEAN NMLKJIHGFEDCBA 2
		TNT1 A 0 A_SetScale(1.0,1.0)
		goto See
	Raise2:
		TNT1 A 0 A_TakeInventory("BD_KillChecker",10)
		HEAP KJIHGFEDCBA 2
		TNT1 A 0 A_SetScale(1.0,1.0)
		goto See
	SawRaise:
		TNT1 A 0 A_TakeInventory("BD_KillChecker",10)
		HEAO GFEDCBA 3
		TNT1 A 0 A_SetScale(1.0,1.0)
		goto See
	XRaise:
		TNT1 A 0 A_TakeInventory("BD_KillChecker",10)
		HEAX RQPONMLKJIHGFEDCBA 1
		TNT1 A 0 A_SetScale(1.0,1.0)
		goto See
	}
}

Actor BD_CacodemonBall : CacodemonBall replaces CacodemonBall
{
+FORCEXYBILLBOARD
+DONTBLAST
decal Scorch
renderstyle Add
alpha 0.9
scale .5
States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf(GetCVAR("Bdoom_debris")==0,3)
		TNT1 AA 0 A_SpawnItemEx("FirePiece_Cacodemon",random(8,12),random(-5,5),random(-5,5),random(1,0),0,0,180,0)
		CABA A 2 bright
		TNT1 A 0 A_JumpIf(GetCVAR("Bdoom_debris")==0,3)
		TNT1 AA 0 A_SpawnItemEx("FirePiece_Cacodemon",random(8,12),random(-5,5),random(-5,5),random(1,0),0,0,180,0)
		CABA B 2 bright 
		TNT1 A 0 A_JumpIf(GetCVAR("Bdoom_debris")==0,3)
		TNT1 AA 0 A_SpawnItemEx("FirePiece_Cacodemon",random(8,12),random(-5,5),random(-5,5),random(1,0),0,0,180,0)
		CABA C 2  bright
		TNT1 A 0 A_JumpIf(GetCVAR("Bdoom_debris")==0,3)
		TNT1 AA 0 A_SpawnItemEx("FirePiece_Cacodemon",random(8,12),random(-5,5),random(-5,5),random(1,0),0,0,180,0)
		CABA D 2  bright
		loop
	Death:
		TNT1 a 0 A_SetScale(1.0,1.0)
		CABX ABCDEF 3  A_FadeOut(0.1)
		stop
		}
}

Actor FirePiece_Cacodemon : FirePiece
{
translation "169:191=192:207", "208:223=169:191"
states
	{
	Spawn:
		FIRP ABCDEFGHIJ 2  A_FadeOut(0.1)
		stop
	}
}