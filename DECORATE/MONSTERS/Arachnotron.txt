Actor BD_Arachnotron : Arachnotron replaces Arachnotron
{

states
	{
	Spawn:
		ARAI AB 10 A_Look
		loop
	Crush:
		CRS2 A -1
		stop
	see:
		ARAC a 20
		TNT1 A 0 A_SetScale(1.0,1.0)
		ARAC a 3 a_babymetal
		ARAC bcdee 3 a_chase
		ARAC f 3 a_babymetal
		ARAC ghijj 3 a_chase
		goto see+1
	missile:
		ARAC a 20 a_facetarget
		ARAT a 3 bright A_BspiAttack
		ARAT b 3 bright
		ARAT c 3 bright a_SpidRefire
		goto missile+1
	pain:
		ARPA a 3
		ARPA b 3 a_pain
		goto see+1
	Death:
		TNT1 A 0 A_SetScale(randompick(-1.0,1.0),1.0)
		ARAM a 3
		ARAM b 3 a_scream
		ARAM c 3 a_noblocking
		ARAM defghI 3 A_SpawnItemEx("MeatPiece",random(-4,4),random(-4,4),random(32,48),	random(1,5),0,random(2,5),random(-70,-100)*scalex,SXF_SETMASTER,40)
		ARAM jkl 3 A_SpawnItemEx("BloodSpurt",random(-4,4),random(-4,4),random(8,36),random(1,3),0,random(1,4),random(-70,-110)*scalex,SXF_SETMASTER)
		TNT1 A 0 A_SpawnItemEx("CorpseFlySpawner",0,0,0,0,0,0,0,SXF_SETMASTER)
		ARAM m -1 a_bossdeath
		stop
	Death.BFGSplash:
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Gibs")==0,"Death")
		TNT1 A 0 Thing_SetTranslation(0,11) //translation to grey+blood
		goto Death.PlasmaDamage+2
	Death.GreenFire:
	Death.Fire:
	Death.PlasmaDamage:
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Gibs")==0,"Death")
		TNT1 A 0 Thing_SetTranslation(0,7)
		TNT1 A 0 A_SetScale(randompick(-1.0,1.0),1.0)
		ARAM a 3 A_SpawnItemEx("BlackSmoke",random(-20,20),random(-20,20),random(16,40),0,0,1)
		ARAM b 3 a_scream
		TNT1 A 0 A_SpawnItemEx("BlackSmoke",random(-20,20),random(-20,20),random(16,40),0,0,1)
		ARAM c 3 a_noblocking
		ARAM defgh 3 A_SpawnItemEx("BlackSmoke",random(-20,20),random(-20,20),random(16,40),0,0,1)
		ARAM ijkl 3 A_SpawnItemEx("BlackSmoke",random(-20,20),random(-20,20),random(16,40),0,0,1)
		ARAM MMMMM 6 A_SpawnItemEx("BlackSmoke",random(-20,20),random(-20,20),random(16,40),0,0,1)
		ARAM MMMMM 9 A_SpawnItemEx("BlackSmoke",random(-20,20),random(-20,20),random(16,40),0,0,1)
		ARAM MMMMM 12 A_SpawnItemEx("BlackSmoke",random(-20,20),random(-20,20),random(16,40),0,0,1)
		ARAM m -1 a_bossdeath
		stop
	Raise:
		TNT1 A 0 A_GiveToChildren("WasGibbed",1)
		TNT1 A 0 Thing_SetTranslation(0,10)
	    ARAM LKJIHGFEDCBA 2
		TNT1 A 0 A_SetScale(1.0,1.0)
		Goto See+1
		}
}

Actor GreenPlasmaball : ArachnotronPlasma replaces ArachnotronPlasma
{
translation "192:207=112:127", "240:247=112:127"
+DONTBLAST
+FORCEXYBILLBOARD
+THRUGHOST
+NOEXTREMEDEATH
+BLOODLESSIMPACT
+SKYEXPLODE
decal ArachnotronPlasmaSplat
damagetype PlasmaDamage
renderstyle Translucent
scale .2
alpha 0.7
SeeSound ""
DeathSound "baby/shotx"
States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_weapons")==1,3)
		TNT1 A 0 A_PlaySound("baby/attack")
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_PlaySound("weapons/plasma/fire")
		TNT1 A 0 A_JumpIf(GetCVAR("Bdoom_debris")==0,2)
		TNT1 A 0 A_SpawnItemEx("GreenFlarePlasma",0,0,0,velx,vely,velz,0,SXF_SETMASTER|SXF_ORIGINATOR|SXF_ABSOLUTEVELOCITY)
		ARBA ABCDEF 3 bright
		goto spawn+7
	Death.Sky:
		TNT1 A 0 A_RemoveChildren(1,RMVF_EVERYTHING)
		stop
	Death:
		TNT1 A 0 A_RemoveChildren(1,RMVF_EVERYTHING)
		TNT1 A 0 A_JumpIf(GetCVAR("Bdoom_debris")==0,"Explosion")
		TNT1 AAAAAAAAAAAA 0 A_SpawnProjectile("GreenPlasmaPiece",0,0,random(-360,360),2,random(-50,50))
		TNT1 A 0 bright A_SpawnItem("WeaponSmoke")
	Explosion:
		TNT1 A 0 A_SetScale(0.2*randompick(-1,1),0.2*randompick(-1,1))
		PEXP ABCDEFGHIJKLM 2 bright A_FadeOut(0.07)
		stop
	}
}

Actor GreenFlarePlasma : Flare_General
{
renderstyle Add
alpha .5
scale .35
states
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf(GetCVAR("Bdoom_debris")==0,"Null")
		TNT1 A 0 A_Warp(AAPTR_MASTER)
		LEGY A 1 bright
		wait
	}
}


Actor GreenPlasmaPiece : DebrisGeneral
{
translation "192:207=112:127", "240:247=112:127"
+DONTSPLASH
-NOGRAVITY
renderstyle Add
alpha 1.0
scale 0.08
speed 6
gravity 0.8
states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(GetCVAR("Bdoom_debris")==0,"Null")
		PLBS A 1 bright A_FadeOut(0.03)
		loop
	}
}