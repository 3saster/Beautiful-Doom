//*******************
//*******************
// LIGHT SOURCES
//*******************
//*******************

///////////////////////
//// TALL TORCHES /////
///////////////////////


//--- Flares

Actor LampFlare_General : Flare_General //since light sources are Y-billboarded, XY flares don't look good with them
{-FORCEXYBILLBOARD}



//// CANDELABRA

Actor YellowFlare_Candelabra : Flare_General
{
alpha .5
scale .5
states
	{
	Spawn:
		TNT1 A 0 NoDelay A_CheckSight(3)
		TNT1 A 0 A_SetScale(0.31+random(1,3)*0.01)
		TNT1 A 0 A_Warp(AAPTR_MASTER,0,0,60,0,WARPF_NOCHECKPOSITION)
		LENY A 2 bright
		loop
	}
}


Actor BD_Candelabra : Candelabra replaces Candelabra
{
+RANDOMIZE
+LOOKALLAROUND
scale .19
states
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf(GetCVAR("bdoom_Flames")==0,"NoHalo")
		TNT1 A 0 A_SpawnItemEx("YellowFlare_Candelabra",	0,0,52,	0,0,0,0,SXF_SETMASTER)
		CBRA ABCDEFGHIJ 3 bright 
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Flames")==0,"NoHalo")
		goto spawn+2
	NoHalo:
		TNT1 A 0 A_RemoveChildren(1,RMVF_EVERYTHING)
		CBRA ABCDEFGHIJ 3 bright
		goto spawn 
	}
}

//// CANDLESTICK

Actor YellowFlare_Candlestick : YellowFlare_Candelabra
{
scale .15
states
	{
	Spawn:
		TNT1 A 0 NoDelay A_CheckSight(3)
		TNT1 A 0 A_SetScale(0.13+random(1,3)*0.005)
		TNT1 A 0 A_Warp(AAPTR_MASTER,0,0,13,0,WARPF_NOCHECKPOSITION)
		LENY A 2 bright
		loop
	}
}

Actor BD_Candlestick : Candlestick replaces Candlestick
{
+RANDOMIZE
+LOOKALLAROUND
scale .22
states
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf(GetCVAR("bdoom_Flames")==0,"NoHalo")
		TNT1 A 0 A_SpawnItemEx("YellowFlare_Candlestick",	0,0,13,	0,0,0,0,SXF_SETMASTER)
		CAND ABCDEFGHIJ 3 bright 
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Flames")==0,"NoHalo")
		goto spawn+2
	NoHalo:
		TNT1 A 0 A_RemoveChildren(1,RMVF_EVERYTHING)
		CAND ABCDEFGHIJ 3 bright
		goto spawn 
	}
}



//******
// TECH
//******

//// BURNING BARREL

Actor YellowFlare_BurningBarrel : YellowFlare_Candelabra
{
scale .3
states
	{
	Spawn:
		TNT1 A 0 NoDelay A_CheckSight(3)
		TNT1 A 0 A_SetScale(0.28+random(0,4)*0.01)
		TNT1 A 0 A_Warp(AAPTR_MASTER,0,0,36,0,WARPF_NOCHECKPOSITION)
		LENY A 2 bright
		loop
	}
}

Actor BD_BurningBarrel : BurningBarrel replaces BurningBarrel
{
+LOOKALLAROUND
height 35
states
	{
	Spawn:
		TNT1 A 0 NoDelay A_PlaySound("torch/burn",0,1.0,1,5)
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Flames")==0,"NoHalo")
		TNT1 A 0 A_SpawnItemEx("YellowFlare_BurningBarrel",	0,0,26,	0,0,0,0,SXF_SETMASTER)
		BURB ABCDEFGHIJKLM 2 bright 
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Flames")==0,"NoHalo")
		goto spawn+3
	NoHalo:
		TNT1 A 0 A_RemoveChildren(1,RMVF_EVERYTHING)
		BURB ABCDEFGHIJKLM 2 bright
		goto spawn+1
	}
}

Actor LampSpark : RicochetSpark
{
scale 0.02
states
	{
	Spawn:
		SPRK A 1 bright A_FadeOut(0.1)
		loop
	}
}

Actor LensFlare_Broken : LampFlare_General
{
alpha 0.5
scale .25
states
	{
	Spawn:
		LENS A 1 bright A_FadeOut(0.1)
		loop
	}
}

Actor LampSparkSpawner
{
+NOINTERACTION
+NOGRAVITY
states
	{
		Spawn:	
			TNT1 A 0 NoDelay A_PlaySound("spark",0,1.0,0,6,0)
			TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx("LampSpark",0,0,0,	random(1,2),0,random(1,3),random(0,360),0,40)
			TNT1 A 0 A_SpawnItemEx("LensFlare_Broken",0,0,0,0,0,0,0)
			stop
	}
}

//// COLUMN (small floor lamp)

Actor LensFlare_Column : Flare_General
{
health 1
alpha 0.75
scale .33
states
	{
	Spawn:
		TNT1 A 0 NoDelay A_CheckSight(3)
		TNT1 A 0 A_SetScale(0.33-random(1,5)*0.01)
		TNT1 A 0 A_Warp(AAPTR_MASTER,0,0,40,0,WARPF_NOCHECKPOSITION)
		LENS A 2 bright
		loop
	}
}

Actor BD_Column : Column replaces Column
{
+DONTBLAST
+VULNERABLE //+SHOOTABLE
mass 999999
+NOBLOOD
health 100
deathheight 48
states
	{
	Spawn:
		TNT1 A 0 NoDelay A_PlaySound("lamp/hum",5,1.0,1,6,0)
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Flames")==0,2)
		TNT1 A 0 A_SpawnItemEx("LensFlare_Column",0,0,40,	0,0,0,0,SXF_SETMASTER)
		BCOL A 50 bright
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Flames")==1,2)
		TNT1 A 0 A_RemoveChildren(1,RMVF_EVERYTHING)
		TNT1 A 0 
		goto spawn+3
	Death:
		TNT1 A 0 A_RemoveChildren(1,RMVF_EVERYTHING)
		BCOL B 1 A_Playsound("glasscrack")
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Debris")==0,12)
		TNT1 AAAAAAAAAAA 0 A_SpawnItemEx("GlassPart",random(-2,2),random(-2,2),random(15,17),	random(4,8),0,random(5,10),	random(0,360),0,80)
	Broken:
		BCOL B 25 A_StopSound(5)
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Flames")==0,"Broken")
		BCOL B 25 A_Jump(64,1)
		BCOL B 25 A_Jump(128,1)
		BCOL B 25 A_Jump(160,1)
		TNT1 A 0 A_SpawnItemEx("LampSparkSpawner",0,0,40,	0,0,0,0,0,64)
		loop
	}
}

Actor GlassPart : BarrelPart
{
bouncefactor 0.2
renderstyle translucent
alpha 0.4
scale 0.2
translation "0:255=4:4"
states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Debris")==0,"Null")
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetScale(-0.2,0.2)
		BPRT ABCDEFGH 2
		goto spawn+4
	Death:
		TNT1 A 0
		stop
	}
}

//// TECHLAMP (tall floor lamp)

Actor LensFlare_Techlamp : LensFlare_Column
{
scale .42
states
	{
	Spawn:
		TNT1 A 0 NoDelay A_CheckSight(3)
		TNT1 A 0 A_SetScale(0.42-random(1,5)*0.01)
		TNT1 A 0 A_Warp(AAPTR_MASTER,0,0,70,0,WARPF_NOCHECKPOSITION)
		LENS A 2 bright //A_Warp(AAPTR_MASTER,0,0,70,0,WARPF_NOCHECKPOSITION)
		loop
	}
}

Actor BD_TechLamp : TechLamp replaces TechLamp
{
+DONTBLAST
+LOOKALLAROUND
+VULNERABLE //+SHOOTABLE
mass 999999
+NOBLOOD
health 100
deathheight 80
states
	{
	Spawn:
		TNT1 A 0 NoDelay A_PlaySound("lamp/hum",5,1.0,1,6,0)
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Flames")==0,"NoHalo")
		TNT1 A 0 A_SpawnItemEx("LensFlare_Techlamp",	0,0,70,	0,0,0,0,SXF_SETMASTER)
		BLMP ABCD 4 bright
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Flames")==0,"NoHalo")
		goto spawn+3
	NoHalo:
		TNT1 A 0 A_RemoveChildren(1,RMVF_EVERYTHING)
		BLMP ABCD 2 bright
		goto spawn 
	Death:
		TNT1 A 0 A_RemoveChildren(1,RMVF_EVERYTHING)
		BLMP E 1 A_Playsound("glasscrack")
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Debris")==0,12)
		TNT1 AAAAAAAAAAA 0 A_SpawnItemEx("GlassPart",random(-2,2),random(-2,2),random(47,49),	random(4,8),0,random(5,10),	random(0,360),0,80)
	Broken:
		BLMP E 25 A_StopSound(5)
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Flames")==0,"Broken")
		BLMP E 25 A_Jump(64,1)
		BLMP E 25 A_Jump(128,1)
		BLMP E 25 A_Jump(160,1)
		TNT1 A 0 A_SpawnItemEx("LampSparkSpawner",0,0,66,	0,0,0,0,0,64)
		loop
	}
}

//// TECHLAMP2 (medium floor lamp)

Actor LensFlare_Techlamp2 : LensFlare_Techlamp
{
states
	{
	Spawn:
		TNT1 A 0 NoDelay A_CheckSight(3)
		TNT1 A 0 A_SetScale(0.33-random(1,5)*0.01)
		TNT1 A 0 A_Warp(AAPTR_MASTER,0,0,52,0,WARPF_NOCHECKPOSITION)
		LENS A 2 bright
		loop
	}
}

Actor BD_TechLamp2 : TechLamp2 replaces TechLamp2
{
+DONTBLAST
+LOOKALLAROUND
+VULNERABLE //+SHOOTABLE
+NOBLOOD
health 100
deathheight 60
mass 999999
states
	{
	Spawn:
		TNT1 A 0 NoDelay A_PlaySound("lamp/hum",5,1.0,1,6,0)
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Flames")==0,"NoHalo")
		TNT1 A 0 A_SpawnItemEx("LensFlare_Techlamp2",	0,0,40,	0,0,0,0,SXF_SETMASTER)
		BLP2 ABCD 4 bright
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Flames")==0,"NoHalo")
		goto spawn+3
	NoHalo:
		TNT1 A 0 A_RemoveChildren(1,RMVF_EVERYTHING)
		BLP2 ABCD 2 bright
		goto spawn 
	Death:
		TNT1 A 0 A_RemoveChildren(1,RMVF_EVERYTHING)
		BLP2 E 1 A_Playsound("glasscrack")
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Debris")==0,12)
		TNT1 AAAAAAAAAAA 0 A_SpawnItemEx("GlassPart",random(-2,2),random(-2,2),random(47,49),	random(4,8),0,random(5,10),	random(0,360),0,80)
	Broken:
		BLP2 E 25 A_StopSound(5)
		TNT1 A 0 A_JumpIf(GetCVAR("bdoom_Flames")==0,"Broken")
		BLP2 E 25 A_Jump(64,1)
		BLP2 E 25 A_Jump(128,1)
		BLP2 E 25 A_Jump(160,1)
		TNT1 A 0 A_SpawnItemEx("LampSparkSpawner",0,0,50,	0,0,0,0,0,64)
		loop
	}
}